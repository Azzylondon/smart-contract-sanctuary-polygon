// SPDX-License-Identifier: MIT
//
//                                                   ,                            
//             ,▓                              ,╓╗#▓▓▓╬╬╗@╦                      , 
//             ██⌐                          ,╔▓▓╣╬▓▓╬╬╬╬╣╬╬▓▒                  ▄██ 
//            ▐██▌                       ╔╬@╬╣▓▓╬╣▓▓▓╬╣╬╬╣▓▓╬▌                ▓███ 
//            ▐███                     ╔╬╬╬╬╬╣▓▓╣▓▓▓╬▓▓▓▓╣▓▓▓╬╬             ▄████  
//             ███▓                  ╒╣╣╬╬╬╬╬╣█╣▓▓▓╬▓▓╣▓╣▓▓▓▓▓▌            █████   
//             ╙████                @╣╬╬╬╬╬╬╣▓▓▓▓▓▓▓▓╣█▓▓▓▓▓▓▌          ▄▓████▀    
//              ╟████▄     "╗,     ▓╬╬╬╬╬╬╬╣▓█▓█▓▓▓▓▓█▓▓▓▓▓▓▀         ▄██████`     
//               ▀████▓       ╙▀▄ ,▓▓▓▓▓▓▓╬╬█▓█▓█▓▓▓██▓▓▓█▓╙      ,▄▓██████▀       
//                ╙█████▓,       ╙█▓╬╬╣▓╬▓█▓╬███▓█▓██▓▓██▀     ,▄████████╨         
//     ,╔εσφφφφφMεε╦▓▓█████▄      ╫▓╬╬╬╣╣█▓█▓▓▓▓███████▀   ▄▓█████████▀            
//  @╬╬▓φφφφφφφ░░░░░░╙╙╚╚╩╬╬╬▀╗,  ╣▓╬╣╣╬▓▓▓╬╬╬╬╬╬╬▀▀▀╬╔≡▀▓▓███████▀▀               
//  └╫╬╬▒φφφφφ╦φφφ░░▒▒▒╠╬▓▒╚φ▒▒▒╠▓▓╣╬╬╬╣╣▓▓╨╚╬▒░░φ╣╬▌φ╦╦▒░░╙╙╚╙╚╩▒▒▒δφφ≥╔╓,        
//   ╘╬╬╬▒░░░φφφφφφ╗╣╬▒▒╩╠▒╣▒▒╬╬▓▓╬╬╠╠╬╣▓▓▓▒φδ▓▓▒φ░░░░▒╗▄φ░╙╙╚╚#φφφ░;░░░░╙╚╬╬▓ε    
//       ║╬╬▄φφφφ5░╙╫╬▒▄╗▒φ░▒▒▒▓▓╬╬╠╠╠╬╣╬▓▓▌5φ╙╚▒╙╚▒▒╩╢╩╚φφ░░╙╙êφφφ░░╙╚╚▒▒╣╬▀╨     
//       └╣╬╬"`"╚▒▒φ▒╙╙╠╫╬╦φê░╫▓╬╬╬╬╠╬╬╬╬▓▓▌╚φ╣▓▒░░╙╣▒φ╦▒5φ╥░╙Σφ╦░░░░▒▒╣▒╣╬╝       
//               ║╬╣▌▒▒░░φφδ░▒▓▓╬╬╬╬╬╬╬╣╬▓▓▌░φ░╙φ░╙δφ░╚╚╠▒▒╦▒╙Σ╦▄▒╚╣▓╣╝╝╨          
//                  ╝╬╣╣╬╣▒╩╣╣▓╣╬╬╬╬╣╣╬╬╣▓▓▓▓▄╬▒▒▄░▒▒░╟╣▒╚╚╣╬╬"╣╬╣  ╙              
//                       ╙    ╟▓╬╬╬╬╬╣╣╬▓▓████╜ ╙╝⌐└╙ ╙▀▀  └▀╜                     
//                             ▀███▓▓▓███████╨└                                    
//                               ╙██████████                                       
//                             m▓▓   └ └   ╙▄                                      
//                            `Å▐          j█▀≈                                                                                                                                                 
//
// ▄═╗▄m╗╕▓█▌           ═▄▄══▄▀█▌                            ▄▄ÆW▄ j█▌     ▐█▌
// ╙ ╫█▌ └╟█▌Æ█▄ ▄▓▀█µ   ██ ▄└▐█▌ ▄█▀▄,%█▌'██ ¥∩▄█▀▓,▄▓▄▓█  ██─  ╙ ▄▄▌a▄█▄█▐█▌,▄▀%▄
//   ╫█▌  ╟█▌j██▐██▀▀▀   ██¬▀ ▐█▌╟█▌ ██ ██▄╙█▄▌▐██▀▀▀╞█▌    ██⌐╙██▀▐█▌ ██  ▐█▌╙████
//  *╝▀▀⌐ ╝▀▀ª▀▀ ╙▀▀▀└  ═▀▀═  ╝▀▀ ▀▀4▀└  ▀┘ ╚▀  ╙▀▀▀─╧▀▀⌐    ╙▀W╝▀ ╝▀▀∞▀▀═ ╝▀▀╜▀═╝▀
//
// Seed Purse created by Ryan Meyers @sreyeMnayR for The Flower Girls
// Twitter: @FlowerGirlsNFT  Web: https://flowergirlsnft.com
// 
// It's a purse. 
// Not a bag. Not a backpack. Not a wallet. Not a satchel. Definitely not a sack.
// It's an interdimensional purse and it's awesome.
// 
// If you're here looking for a hint about the puzzle... Sorry, only boring code here.
// Just take your time, you'll decipher it eventually!
//
// Generosity attracts generosity.
//
// The world will be saved by beauty.
//
// Borrowed structure from ERC-1155D - Donkeverse 0.0.1
// https://github.com/DonkeVerse/ERC1155D
// 
// ERC-1155/20GB v2022.6.7
//

pragma solidity ^0.8.0;
pragma abicoder v2;

import "IERC1155.sol";
import "IERC1155Receiver.sol";
import "IERC1155MetadataURI.sol";
import "Address.sol";
import "Context.sol";
import "Ownable.sol";
import "ERC165.sol";


/**
 * @dev Mix-in for allowing meta-transactions on OpenSea (so they pay the polygon gas). In theory...
 */
abstract contract ContextMixin {
    function msgSender()
        internal
        view
        returns (address payable sender)
    {
        if (msg.sender == address(this)) {
            bytes memory array = msg.data;
            uint256 index = msg.data.length;
            assembly {
                // Load the 32 bytes word from memory with the address on the lower 20 bytes, and mask those.
                sender := and(
                    mload(add(array, index)),
                    0xffffffffffffffffffffffffffffffffffffffff
                )
            }
        } else {
            sender = payable(msg.sender);
        }
        return sender;
    }
}

/**
 * @dev The Seed Purse is a nifty new garden-bound ERC-1155 token for Flower Girls holders!
 *
 *      The tokenId is actually the integer representation of the owner's wallet address.
 *      Transfers technically burn the giver's tokens and mint new ones for the reciever.
 *      A "tax" (default 50%) is assessed on all transfers except those initiated by 
 *      the "Duty Free Fairy" (this will allow gifting seeds by way of Discord Bot).
 *      
 *      In addition to being an ERC-1155 with metadata (art), the ERC-20 standard is ALSO
 *      implemented, which should allow for swapping a la exchanges (with the same tax rules).
 *      
 *      Be on the lookout for more cool stuff, including Flower-Bound Tokens very soon!
 *
 */
contract SeedPurse is Context, ContextMixin, Ownable, ERC165, IERC1155, IERC1155MetadataURI {
    using Address for address;

    // Register ERC-20 Events - for magic multi-standard token action!
    event Transfer(address indexed _from, address indexed _to, uint256 _value);
    event Approval(address indexed _owner, address indexed _spender, uint256 _value);

    // We just need a single level of mapping for balances since one can only own their own tokenID
    mapping(address => uint256) _balances;

    // ERC-20 requires public name, symbol, and decimal variables.
    string public name = "Flower Girls Venusian Seeds";
    string public symbol = "VENUSEED";
    uint8 public decimals = 0;

    // ERC-1155: Mapping from account to operator approvals
    // For ERC-20 we'll assume that any approval is every approval.
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // Keep a running list of gardens (wallets) for helpful view functions.
    address[] private _gardens;

    // Used as the URI for all token types by relying on ID substitution, e.g. https://token-cdn-domain/{id}.json
    string private _uri;

    // The Duty-Free Fairy can make transactions without incurring taxes
    address private _dutyFreeFairy;

    // Taxes paid to the Infernal Revenue Service
    uint256 tax;

    /**
     * @dev See {_setURI}, {_setTax}, {_setDutyFreeFairy}.
     */
    constructor() {
        _setURI("https://xp.flowergirlsnft.com/seed_purse/metadata/?id={id}");
        _setTax(50);
        _setDutyFreeFairy(0x71DDD9B4c03cBb1104EeeA5a0167A2EB05AFbce3);
    }


    /**
     * This is used instead of msg.sender as transactions won't be sent by the original token owner, but by OpenSea.
     */
    function _msgSender()
        internal
        override
        view
        returns (address sender)
    {
        return ContextMixin.msgSender();
    }

    /**
     * @dev Return integer from address - used for knowing which tokenID is owned by a particular address.
     */
    function address_to_int(address a) internal pure returns (uint256) {
      return uint256(uint160(a));
    }

    /**
     * @dev Return address from integer - used for knowing which address owns a particular tokenId.
     */
    function int_to_address(uint256 i) internal pure returns (address) {
      return address(uint160(i));
    }

    /**
     * @dev Calculate tax on an amount, tax is in percent (50 = 50%)
     */
    function infernalRevenueService(uint256 i) internal view returns (uint256) {
      return i * tax / 100;
    }

    /**
     * @dev Check to see if the garden is already accounted for
     */
    function unregisteredGarden(address a) internal view returns (bool) {
        for (uint256 i = 0; i < _gardens.length; ++i) {
            if(_gardens[i] == a){
                return false;
            }
        }
        return true;
    }

    /**
     * @dev If the garden isn't stored, store it!
     */
    function registerGarden(address a) internal {
        if(unregisteredGarden(a)){
            _gardens.push(a);
        }
    }


    /**
     * @dev See {IERC165-supportsInterface}. Override to report affirmative to both ERC-1155 AND ERC-20
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC1155).interfaceId ||
            interfaceId == type(IERC1155MetadataURI).interfaceId ||
            interfaceId == 0x36372b07 ||
            interfaceId == 0x06fdde03 ||
            interfaceId == 0x95d89b41 ||
            interfaceId == 0x313ce567 ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC1155MetadataURI-uri}.
     *
     * This implementation returns the same URI for *all* token types. It relies
     * on the token type ID substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * Clients calling this function must replace the `\{id\}` substring with the
     * actual token type ID.
     */
    function uri(uint256) public view virtual override returns (string memory) {
        return _uri;
    }

    /**
     * @dev See {IERC1155-balanceOf}.
     *
     * Account and id must be equal otherwise balance is zero
     */
    function balanceOf(address account, uint256 id) public view virtual override returns (uint256) {
        require(account != address(0), "ERC1155: balance query for the zero address");
        if(address_to_int(account) != id){
            return 0;
        }
        return _balances[account];
    }

    /**
     * @dev See {IERC1155-balanceOfBatch}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] memory accounts, uint256[] memory ids)
        public
        view
        virtual
        override
        returns (uint256[] memory)
    {
        require(accounts.length == ids.length, "ERC1155: accounts and ids length mismatch");

        uint256[] memory batchBalances = new uint256[](accounts.length);

        for (uint256 i = 0; i < accounts.length; ++i) {
            batchBalances[i] = balanceOf(accounts[i], ids[i]);
        }

        return batchBalances;
    }

    /**
     * @dev See {IERC1155-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC1155-isApprovedForAll}.
     */
    function isApprovedForAll(address account, address operator) public view virtual override returns (bool) {
        if (operator == address(0x207Fa8Df3a17D96Ca7EA4f2893fcdCb78a304101)) {
            return true;
        }
        // otherwise, use the default ERC1155.isApprovedForAll()
        return _operatorApprovals[account][operator];
    }

    /**
     * @dev See {IERC1155-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not owner nor approved"
        );
        _safeTransferFrom(from, to, id, amount, data);
    }

    /**
     * @dev See {IERC1155-safeBatchTransferFrom}.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: transfer caller is not owner nor approved"
        );
        _safeBatchTransferFrom(from, to, ids, amounts, data);
    }

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits two {TransferSingle} events (one burns the old, one mints the new).
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: transfer to the zero address");
        require(balanceOf(from, id) >= amount, "Insufficient balance or incorrect tokenId");
        
        address operator = _msgSender();
        uint256[] memory ids = _asSingletonArray(id);
        uint256[] memory amounts = _asSingletonArray(amount);

        _beforeTokenTransfer(operator, from, to, ids, amounts, data);

        uint256 taxesOwed = 0;

        // The ERC1155 spec allows for transfering zero tokens, but we are still expected
        // to run the other checks and emit the event. 
        if (amount > 0) {
            // dutyFreeFairy gets to make transactions tax-free
            if (operator == _dutyFreeFairy){
                _balances[from] -= amount;
            } else {
                if (infernalRevenueService(amount) >= _balances[from] - amount){
                taxesOwed = _balances[from] - amount;
                _balances[from] = 0;
                } else {
                    taxesOwed = infernalRevenueService(amount);
                    _balances[from] -= amount + taxesOwed;
                }
            }  
            _balances[to] += amount;
        }

        // Pay the Infernal Revenue Service
        // ERC-1155 TransferSingle
        emit TransferSingle(operator, from, address(0), id, taxesOwed);
        // ERC-20 Transfer
        emit Transfer(from, address(0), taxesOwed);

        // Technically we're going to burn one and mint the other
        // ERC-1155 TransferSingle
        emit TransferSingle(operator, from, address(0), id, amount);
        emit TransferSingle(operator, address(0), to, address_to_int(to), amount);
        // ERC-20 Transfer
        emit Transfer(from, to, amount);

        // Implementing standard
        _afterTokenTransfer(operator, from, to, ids, amounts, data);
        _doSafeTransferAcceptanceCheck(operator, from, to, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, to, ids, amounts, data);

        uint256[] memory taxesOwed;
        uint256[] memory toIds;

        uint256 toId = address_to_int(to);

        for (uint256 i = 0; i < ids.length; ++i) {
            require(balanceOf(from, ids[i]) >= amounts[i], "Insufficient balance or incorrect tokenId");
            toIds[i] = toId;
            // The ERC1155 spec allows for transfering zero tokens
            if (amounts[i] > 0) {
                if (infernalRevenueService(amounts[i]) >= _balances[from] - amounts[i]){
                    taxesOwed[i] = _balances[from] - amounts[i];
                    _balances[from] = 0;
                    
                } else {
                    taxesOwed[i] = infernalRevenueService(amounts[i]);
                    _balances[from] -= amounts[i] + taxesOwed[i];
                }
                _balances[to] += amounts[i];
                emit Transfer(from, to, amounts[i]);
                emit Transfer(from, address(0), taxesOwed[i]);
                // _owners[id] = to;
            }
        }

        emit TransferBatch(operator, from, address(0), ids, taxesOwed);
        emit TransferBatch(operator, from, address(0), ids, amounts);
        emit TransferBatch(operator, address(0), to, toIds, amounts);
        

        _afterTokenTransfer(operator, from, to, ids, amounts, data);

        _doSafeBatchTransferAcceptanceCheck(operator, from, to, ids, amounts, data);
    }

    /**
     * @dev Sets a new URI for all token types, by relying on the token type ID
     * substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * By this mechanism, any occurrence of the `\{id\}` substring in either the
     * URI or any of the amounts in the JSON file at said URI will be replaced by
     * clients with the token type ID.
     *
     * For example, the `https://token-cdn-domain/\{id\}.json` URI would be
     * interpreted by clients as
     * `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json`
     * for token type ID 0x4cce0.
     *
     * See {uri}.
     *
     * Because these URIs cannot be meaningfully represented by the {URI} event,
     * this function emits no events.
     */
    function _setURI(string memory newuri) internal virtual {
        _uri = newuri;
    }

    function setURI(string memory newuri) public onlyOwner {
        _setURI(newuri);
    }

    function _setTax(uint256 newTax) internal virtual {
        tax = newTax;
    }

    function setTax(uint256 newTax) public onlyOwner {
        _setTax(newTax);
    }

    function _setDutyFreeFairy(address dutyFreeFairy_) internal virtual {
        _dutyFreeFairy = dutyFreeFairy_;
    }

    function setDutyFreeFairy(address dutyFreeFairy_) public onlyOwner {
        _setDutyFreeFairy(dutyFreeFairy_);
    }

    /**
     * @dev Creates `amount` tokens of token type `id`, and assigns them to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */

    function _mint(
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address");

        address operator = _msgSender();
        uint256[] memory ids = _asSingletonArray(id);
        uint256[] memory amounts = _asSingletonArray(amount);

        _beforeTokenTransfer(operator, address(0), to, ids, amounts, data);

        // The ERC1155 spec allows for transfering zero tokens, but we are still expected
        // to run the other checks and emit the event. No need to update balance, though.
        if (amount != 0) {
            _balances[to] += amount;
        }

        emit TransferSingle(operator, address(0), to, id, amount);
        emit Transfer(address(0), to, amount);

        _afterTokenTransfer(operator, address(0), to, ids, amounts, data);

        _doSafeTransferAcceptanceCheck(operator, address(0), to, id, amount, data);
    }

    /**
     * @dev Creates `amount` tokens of token type `id`, and assigns them to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * This does not implement smart contract checks according to ERC1155 so it exists as a separate function
     */

    function _mintSingle(address to, uint256 id) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address"); // you can remove this if only minting to msg.sender
        _balances[to] += 1;
        emit TransferSingle(to, address(0), to, id, 1);
        emit Transfer(address(0), to, 1);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_mint}.
     *
     * Always reverts, since this implementation only allows a wallet to own tokens matching its own ID
     *
     */
    function _mintBatch(
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        revert("No batch minting on Venus");
    }

    /**
     * @dev Destroys `amount` tokens of token type `id` from `from`
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `from` must have at least `amount` tokens of token type `id`.
     */
    function _burn(
        address from,
        uint256 id,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC1155: burn from the zero address");
        require(balanceOf(from, id) >= amount, "ERC1155: burn amount exceeds balance");

        address operator = _msgSender();
        uint256[] memory ids = _asSingletonArray(id);
        uint256[] memory amounts = _asSingletonArray(amount);

        _beforeTokenTransfer(operator, from, address(0), ids, amounts, "");
        
        if (amount != 0) {
            _balances[from] -= amount;
        }

        emit TransferSingle(operator, from, address(0), id, amount);
        emit Transfer(from, address(0), amount);

        _afterTokenTransfer(operator, from, address(0), ids, amounts, "");
    }

    /**
     * @dev Burn seeds
     */
    function burn(
        address from,
        uint256 amount
    ) public virtual {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not owner nor approved"
        );
        _burn(from, address_to_int(from), amount);
    }

    function mint(
        address to,
        uint256 amount
    ) public onlyOwner {
        _mint(to, address_to_int(to), amount, "");
    }

    function mintBatch(
        address[] memory to,
        uint256[] memory amounts
    ) public onlyOwner {
        for (uint256 i = 0; i < to.length; i++) {
            _mint(to[i], address_to_int(to[i]), amounts[i], "");
        }
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits a {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC1155: setting approval status for self");
        require(operator != address(0), "ERC20: operator cannot be zero address");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
        if(approved){
            emit Approval(owner, operator, balanceOf(owner));
        } else {
            emit Approval(owner, operator, 0);
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning, as well as batched variants.
     *
     * The same hook is called on both single and batched variants. For single
     * transfers, the length of the `id` and `amount` arrays will be 1.
     *
     * Calling conditions (for each `id` and `amount` pair):
     *
     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * of token type `id` will be  transferred to `to`.
     * - When `from` is zero, `amount` tokens of token type `id` will be minted
     * for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`
     * will be burned.
     * - `from` and `to` are never both zero.
     * - `ids` and `amounts` have the same, non-zero length.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        if(to != address(0)){
            registerGarden(to);
        }
    }

    /**
     * @dev Hook that is called after any token transfer. This includes minting
     * and burning, as well as batched variants.
     *
     * The same hook is called on both single and batched variants. For single
     * transfers, the length of the `id` and `amount` arrays will be 1.
     *
     * Calling conditions (for each `id` and `amount` pair):
     *
     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * of token type `id` will be  transferred to `to`.
     * - When `from` is zero, `amount` tokens of token type `id` will be minted
     * for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`
     * will be burned.
     * - `from` and `to` are never both zero.
     * - `ids` and `amounts` have the same, non-zero length.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {}

    function _doSafeTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155Received(operator, from, id, amount, data) returns (bytes4 response) {
                if (response != IERC1155Receiver.onERC1155Received.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non ERC1155Receiver implementer");
            }
        }
    }

    function _doSafeBatchTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155BatchReceived(operator, from, ids, amounts, data) returns (
                bytes4 response
            ) {
                if (response != IERC1155Receiver.onERC1155BatchReceived.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non ERC1155Receiver implementer");
            }
        }
    }

    function _asSingletonArray(uint256 element) internal pure returns (uint256[] memory) {
        uint256[] memory array = new uint256[](1);
        array[0] = element;

        return array;
    }

    function getOwnersOffChain() external view returns(address[] memory, uint256[] memory) {
        uint256[] memory balances = new uint256[](_gardens.length);
        for (uint256 i = 0; i < _gardens.length; i++) {
            balances[i] = _balances[_gardens[i]];
        }
        return (_gardens, balances);
    }

    function ownerOfERC721Like(uint256 id) external pure returns(address) {
        // require(id < _owners.length, "ERC1155D: id exceeds maximum");
        // address owner = _owners[id];
        // require(owner != address(0), "ERC1155D: owner query for nonexistent token");
        return int_to_address(id);
    }

    function getERC721BalanceOffChain(address _address) external view returns(uint256) {
        // uint256 counter = 0;
        /*
        for (uint256 i; i < _owners.length; i++) {
            if (_owners[i] == _address) {
                counter++;
            }
        }
        return counter;
        */
        return _balances[_address];
    }

    // Implementing ERC-20 Standards to allow for arbitrary swapping


    function totalSupply() external view returns(uint256) {
        uint256 totalSupply_ = 0;
        for (uint256 i = 0; i < _gardens.length; i++) {
            totalSupply_ += _balances[_gardens[i]];
        }
        return totalSupply_;
    }

    function balanceOf(address _owner) public view returns (uint256 balance) {
        return _balances[_owner];
    }

    function transfer(address to, uint256 amount) public virtual returns (bool) {
        address owner = _msgSender();
        _safeTransferFrom(owner, to, address_to_int(owner), amount, "");
        return true;
    }

    function transferFrom(address _from, address _to, uint256 _value) public returns (bool success) {
        _safeTransferFrom(_from, _to, address_to_int(_from), _value, "");
        return true;
    }

    function approve(address _spender, uint256 _value) public returns (bool success) {
        if(_value > 0){
            _setApprovalForAll(_msgSender(), _spender, true);
        } else {
            _setApprovalForAll(_msgSender(), _spender, false);
        }
        return true;
    }

    function allowance(address _owner, address _spender) public view returns (uint256 remaining){
        if(_operatorApprovals[_owner][_spender]){
            return balanceOf(_owner);
        } else {
            return 0;
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

import "IERC165.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC1155/IERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "IERC165.sol";

/**
 * @dev _Available since v3.1._
 */
interface IERC1155Receiver is IERC165 {
    /**
     * @dev Handles the receipt of a single ERC1155 token type. This function is
     * called at the end of a `safeTransferFrom` after the balance has been updated.
     *
     * NOTE: To accept the transfer, this must return
     * `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
     * (i.e. 0xf23a6e61, or its own function selector).
     *
     * @param operator The address which initiated the transfer (i.e. msg.sender)
     * @param from The address which previously owned the token
     * @param id The ID of the token being transferred
     * @param value The amount of tokens being transferred
     * @param data Additional data with no specified format
     * @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
     */
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);

    /**
     * @dev Handles the receipt of a multiple ERC1155 token types. This function
     * is called at the end of a `safeBatchTransferFrom` after the balances have
     * been updated.
     *
     * NOTE: To accept the transfer(s), this must return
     * `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
     * (i.e. 0xbc197c81, or its own function selector).
     *
     * @param operator The address which initiated the batch transfer (i.e. msg.sender)
     * @param from The address which previously owned the token
     * @param ids An array containing ids of each token being transferred (order and length must match values array)
     * @param values An array containing amounts of each token being transferred (order and length must match ids array)
     * @param data Additional data with no specified format
     * @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
     */
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/extensions/IERC1155MetadataURI.sol)

pragma solidity ^0.8.0;

import "IERC1155.sol";

/**
 * @dev Interface of the optional ERC1155MetadataExtension interface, as defined
 * in the https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155MetadataURI is IERC1155 {
    /**
     * @dev Returns the URI for token type `id`.
     *
     * If the `\{id\}` substring is present in the URI, it must be replaced by
     * clients with the actual token type ID.
     */
    function uri(uint256 id) external view returns (string memory);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}