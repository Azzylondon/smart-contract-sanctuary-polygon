// SPDX-License-Identifier: GPL-2.0
pragma solidity 0.7.6;
pragma abicoder v2;

/******************************************************************************\
* EIP-2535 Diamonds: https://eips.ethereum.org/EIPS/eip-2535
/******************************************************************************/

import {IDiamondCut} from '../interfaces/IDiamondCut.sol';
import './utils/Storage.sol';

contract DiamondCutFacet is IDiamondCut {
    /// @notice Add/replace/remove any number of functions and optionally execute
    ///         a function with delegatecall
    /// @param _diamondCut Contains the facet addresses and function selectors
    /// @param _init The address of the contract or facet to execute _calldata
    /// @param _calldata A function call, including function selector and arguments
    ///                  _calldata is executed with delegatecall on _init
    function diamondCut(
        FacetCut[] calldata _diamondCut,
        address _init,
        bytes calldata _calldata
    ) external override {
        PositionManagerStorage.enforceIsGovernance();
        PositionManagerStorage.diamondCut(_diamondCut, _init, _calldata);
    }
}

// SPDX-License-Identifier: GPL-2.0
pragma solidity 0.7.6;
pragma abicoder v2;

import '../../interfaces/IPositionManager.sol';
import '../../interfaces/IUniswapAddressHolder.sol';
import '../../interfaces/IAaveAddressHolder.sol';
import '../../interfaces/IDiamondCut.sol';
import '../../interfaces/IRegistry.sol';

struct FacetAddressAndPosition {
    address facetAddress;
    uint96 functionSelectorPosition; // position in facetFunctionSelectors.functionSelectors array
}

struct FacetFunctionSelectors {
    bytes4[] functionSelectors;
    uint256 facetAddressPosition; // position of facetAddress in facetAddresses array
}

struct AavePositions {
    uint256 id;
    address tokenToAave;
}

struct StorageStruct {
    // maps function selector to the facet address and
    // the position of the selector in the facetFunctionSelectors.selectors array
    mapping(bytes4 => FacetAddressAndPosition) selectorToFacetAndPosition;
    // maps facet addresses to function selectors
    mapping(address => FacetFunctionSelectors) facetFunctionSelectors;
    // facet addresses
    address[] facetAddresses;
    IUniswapAddressHolder uniswapAddressHolder;
    address owner;
    IRegistry registry;
    IAaveAddressHolder aaveAddressHolder;
    mapping(bytes32 => bytes32) storageVars;
}

library PositionManagerStorage {
    bytes32 private constant key = keccak256('position-manager-storage-location');

    ///@notice get the storage from memory location
    ///@return s the storage struct
    function getStorage() internal pure returns (StorageStruct storage s) {
        bytes32 k = key;
        assembly {
            s.slot := k
        }
    }

    ///@notice emitted when a contract changes ownership
    ///@param previousOwner previous owner of the contract
    ///@param newOwner new owner of the contract
    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    ///@notice set the owner field on the storage struct
    ///@param _newOwner new owner of the storage struct
    function setContractOwner(address _newOwner) internal {
        require(_newOwner != address(0), 'SNO');
        StorageStruct storage ds = getStorage();
        address previousOwner = ds.owner;
        ds.owner = _newOwner;
        if (_newOwner != previousOwner) {
            emit OwnershipTransferred(previousOwner, _newOwner);
        }
    }

    ///@notice make sure that a function is called by the PositionManagerFactory contract
    function enforceIsGovernance() internal view {
        StorageStruct storage ds = getStorage();
        require(msg.sender == ds.registry.positionManagerFactoryAddress(), 'SMF');
    }

    ///@notice emitted when a facet is cut into the diamond
    ///@param _diamondCut facet cut
    ///@param _init diamond cut init address
    ///@param _calldata facet cut calldata
    event DiamondCut(IDiamondCut.FacetCut[] _diamondCut, address _init, bytes _calldata);

    ///@notice Internal function version of diamondCut
    ///@param _diamondCut facet cut
    ///@param _init diamond cut init address
    ///@param _calldata facet cut calldata
    function diamondCut(
        IDiamondCut.FacetCut[] memory _diamondCut,
        address _init,
        bytes memory _calldata
    ) internal {
        uint256 _diamondCutLength = _diamondCut.length;
        for (uint256 facetIndex; facetIndex < _diamondCutLength; ++facetIndex) {
            IDiamondCut.FacetCutAction action = _diamondCut[facetIndex].action;
            if (action == IDiamondCut.FacetCutAction.Add) {
                addFunctions(_diamondCut[facetIndex].facetAddress, _diamondCut[facetIndex].functionSelectors);
            } else if (action == IDiamondCut.FacetCutAction.Replace) {
                replaceFunctions(_diamondCut[facetIndex].facetAddress, _diamondCut[facetIndex].functionSelectors);
            } else if (action == IDiamondCut.FacetCutAction.Remove) {
                removeFunctions(_diamondCut[facetIndex].facetAddress, _diamondCut[facetIndex].functionSelectors);
            } else {
                revert('SIF');
            }
        }
        emit DiamondCut(_diamondCut, _init, _calldata);
        initializeDiamondCut(_init, _calldata);
    }

    ///@notice Add functions to facet
    ///@param _facetAddress address of the facet
    ///@param _functionSelectors function selectors to add
    function addFunctions(address _facetAddress, bytes4[] memory _functionSelectors) internal {
        require(_functionSelectors.length != 0, 'SNS');
        StorageStruct storage ds = getStorage();
        require(_facetAddress != address(0), 'SA0');
        uint96 selectorPosition = uint96(ds.facetFunctionSelectors[_facetAddress].functionSelectors.length);

        // add new facet address if it does not exist
        if (selectorPosition == 0) {
            addFacet(ds, _facetAddress);
        }

        uint256 _functionSelectorsLength = _functionSelectors.length;
        for (uint256 selectorIndex; selectorIndex < _functionSelectorsLength; ++selectorIndex) {
            bytes4 selector = _functionSelectors[selectorIndex];
            address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
            require(oldFacetAddress == address(0), 'SFE');
            addFunction(ds, selector, selectorPosition, _facetAddress);
            selectorPosition++;
        }
    }

    ///@notice Add facet by address
    ///@param ds storage struct
    ///@param _facetAddress address of the facet
    function addFacet(StorageStruct storage ds, address _facetAddress) internal {
        ds.facetFunctionSelectors[_facetAddress].facetAddressPosition = ds.facetAddresses.length;
        ds.facetAddresses.push(_facetAddress);
    }

    ///@notice Add single function to facet
    ///@param ds storage struct
    ///@param _selector function selector to add
    ///@param _selectorPosition position of the function selector in the facetFunctionSelectors array
    function addFunction(
        StorageStruct storage ds,
        bytes4 _selector,
        uint96 _selectorPosition,
        address _facetAddress
    ) internal {
        ds.selectorToFacetAndPosition[_selector].functionSelectorPosition = _selectorPosition;
        ds.facetFunctionSelectors[_facetAddress].functionSelectors.push(_selector);
        ds.selectorToFacetAndPosition[_selector].facetAddress = _facetAddress;
    }

    ///@notice Remove single function from facet
    ///@param ds storage struct
    ///@param _facetAddress address of the facet
    ///@param _selector function selector to remove
    function removeFunction(
        StorageStruct storage ds,
        address _facetAddress,
        bytes4 _selector
    ) internal {
        require(_facetAddress != address(0), 'SRE');
        require(_facetAddress != address(this), 'SRI');

        // replace selector with last selector, then delete last selector
        uint256 selectorPosition = ds.selectorToFacetAndPosition[_selector].functionSelectorPosition;
        uint256 lastSelectorPosition = ds.facetFunctionSelectors[_facetAddress].functionSelectors.length - 1;

        // if not the same then replace _selector with lastSelector
        if (selectorPosition != lastSelectorPosition) {
            bytes4 lastSelector = ds.facetFunctionSelectors[_facetAddress].functionSelectors[lastSelectorPosition];
            ds.facetFunctionSelectors[_facetAddress].functionSelectors[selectorPosition] = lastSelector;
            ds.selectorToFacetAndPosition[lastSelector].functionSelectorPosition = uint96(selectorPosition);
        }

        // delete the last selector
        ds.facetFunctionSelectors[_facetAddress].functionSelectors.pop();
        delete ds.selectorToFacetAndPosition[_selector];

        // if no more selectors for facet address then delete the facet address
        if (lastSelectorPosition == 0) {
            // replace facet address with last facet address and delete last facet address
            uint256 lastFacetAddressPosition = ds.facetAddresses.length - 1;
            uint256 facetAddressPosition = ds.facetFunctionSelectors[_facetAddress].facetAddressPosition;
            if (facetAddressPosition != lastFacetAddressPosition) {
                address lastFacetAddress = ds.facetAddresses[lastFacetAddressPosition];
                ds.facetAddresses[facetAddressPosition] = lastFacetAddress;
                ds.facetFunctionSelectors[lastFacetAddress].facetAddressPosition = facetAddressPosition;
            }
            ds.facetAddresses.pop();
            delete ds.facetFunctionSelectors[_facetAddress].facetAddressPosition;
        }
    }

    ///@notice Replace functions in facet
    ///@param _facetAddress address of the facet
    ///@param _functionSelectors function selectors to replace
    function replaceFunctions(address _facetAddress, bytes4[] memory _functionSelectors) internal {
        require(_functionSelectors.length != 0, 'SRF');
        StorageStruct storage ds = getStorage();
        require(_facetAddress != address(0), 'SR0');
        uint96 selectorPosition = uint96(ds.facetFunctionSelectors[_facetAddress].functionSelectors.length);

        // add new facet address if it does not exist
        if (selectorPosition == 0) {
            addFacet(ds, _facetAddress);
        }

        uint256 _functionSelectorsLength = _functionSelectors.length;
        for (uint256 selectorIndex; selectorIndex < _functionSelectorsLength; ++selectorIndex) {
            bytes4 selector = _functionSelectors[selectorIndex];
            address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;

            require(oldFacetAddress != _facetAddress, 'SRR');

            removeFunction(ds, oldFacetAddress, selector);
            addFunction(ds, selector, selectorPosition, _facetAddress);
            selectorPosition++;
        }
    }

    ///@notice remove functions in facet
    ///@param _facetAddress address of the facet
    ///@param _functionSelectors function selectors to remove
    function removeFunctions(address _facetAddress, bytes4[] memory _functionSelectors) internal {
        require(_functionSelectors.length != 0, 'SES');

        StorageStruct storage ds = getStorage();

        require(_facetAddress == address(0), 'SE0');

        uint256 _functionSelectorsLength = _functionSelectors.length;
        for (uint256 selectorIndex; selectorIndex < _functionSelectorsLength; ++selectorIndex) {
            bytes4 selector = _functionSelectors[selectorIndex];
            address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
            removeFunction(ds, oldFacetAddress, selector);
        }
    }

    ///@notice Initialize the diamond cut
    ///@param _init delegatecall address
    ///@param _calldata delegatecall data
    function initializeDiamondCut(address _init, bytes memory _calldata) internal {
        if (_init == address(0)) {
            require(_calldata.length == 0, 'SI0');
        } else {
            require(_calldata.length != 0, 'SIC');

            (bool success, bytes memory error) = _init.delegatecall(_calldata);

            if (!success) {
                if (error.length != 0) {
                    revert(string(error));
                } else {
                    revert('SIR');
                }
            }
        }
    }

    ///@notice check to verify that the key is valid and already whitelisted by governance
    ///@param hashedKey key to check
    modifier verifyKey(bytes32 hashedKey) {
        StorageStruct storage ds = getStorage();

        bytes32 storageVariableHash = ds.storageVars[hashedKey];

        require(storageVariableHash == bytes32(uint256(1)), 'SDK');
        _;
    }

    ///@notice get a specific slot of memory by the given key and read the first 32 bytes
    ///@param hashedKey key to read from
    function getDynamicStorageValue(bytes32 hashedKey) internal view verifyKey(hashedKey) returns (bytes32 value) {
        assembly {
            value := sload(hashedKey)
        }
    }

    ///@dev supposing we've already set the key on the mapping, we can't insert a wrong key
    ///@notice set a specific slot of memory by the given key and write the first 32 bytes
    ///@param hashedKey key to write to
    ///@param value value to write
    function setDynamicStorageValue(bytes32 hashedKey, bytes32 value) internal verifyKey(hashedKey) {
        assembly {
            sstore(hashedKey, value)
        }
    }

    ///@notice add a new hashedKey to the mapping in storage, sort of whitelist
    ///@param hashedKey key to add to the mapping
    function addDynamicStorageKey(bytes32 hashedKey) internal {
        StorageStruct storage ds = getStorage();

        bytes32 storageVariableHash = ds.storageVars[hashedKey];

        ///@dev return if the key already exists
        if (storageVariableHash != bytes32(0)) return;

        ds.storageVars[hashedKey] = bytes32(uint256(1));
    }
}

// SPDX-License-Identifier: GPL-2.0

pragma solidity 0.7.6;

interface IAaveAddressHolder {
    ///@notice default getter for lendingPoolAddress
    ///@return address The address of the lending pool from aave
    function lendingPoolAddress() external view returns (address);

    ///@notice Set the address of lending pool
    ///@param newAddress new address of the lending pool from aave
    function setLendingPoolAddress(address newAddress) external;

    ///@notice Set the address of the registry
    ///@param newAddress The address of the registry
    function setRegistry(address newAddress) external;
}

// SPDX-License-Identifier: GPL-2.0
pragma solidity 0.7.6;
pragma abicoder v2;

interface IDiamondCut {
    enum FacetCutAction {
        Add,
        Replace,
        Remove
    }
    // Add=0, Replace=1, Remove=2

    struct FacetCut {
        address facetAddress;
        FacetCutAction action;
        bytes4[] functionSelectors;
    }

    /// @notice Add/replace/remove any number of functions and optionally execute
    ///         a function with delegatecall
    /// @param _diamondCut Contains the facet addresses and function selectors
    /// @param _init The address of the contract or facet to execute _calldata
    /// @param _calldata A function call, including function selector and arguments
    ///                  _calldata is executed with delegatecall on _init
    function diamondCut(
        FacetCut[] calldata _diamondCut,
        address _init,
        bytes calldata _calldata
    ) external;

    event DiamondCut(FacetCut[] _diamondCut, address _init, bytes _calldata);
}

// SPDX-License-Identifier: GPL-2.0

pragma solidity 0.7.6;
pragma abicoder v2;

interface IPositionManager {
    struct ModuleInfo {
        bool isActive;
        bytes32 data;
    }

    struct AaveReserve {
        mapping(uint256 => uint256) positionShares;
        mapping(uint256 => uint256) tokenIds;
        uint256 sharesEmitted;
    }

    function toggleModule(
        uint256 tokenId,
        address moduleAddress,
        bool activated
    ) external;

    function setModuleData(
        uint256 tokenId,
        address moduleAddress,
        bytes32 data
    ) external;

    function getModuleInfo(uint256 _tokenId, address _moduleAddress)
        external
        view
        returns (bool isActive, bytes32 data);

    function withdrawERC20(address tokenAddress) external;

    function middlewareUniswap(uint256 tokenId, uint256 oldTokenId) external;

    function getAllUniPositions() external view returns (uint256[] memory);

    function getAaveDataFromTokenId(uint256 tokenId) external returns (uint256, address);

    function getOwner() external view returns (address);
}

// SPDX-License-Identifier: GPL-2.0
pragma solidity 0.7.6;
pragma abicoder v2;

interface IRegistry {
    struct Entry {
        address contractAddress;
        bool activated;
        bytes32 defaultData;
        bool activatedByDefault;
    }

    ///@notice return the address of PositionManagerFactory
    ///@return address of PositionManagerFactory
    function positionManagerFactoryAddress() external view returns (address);

    ///@notice return the address of Governance
    ///@return address of Governance
    function governance() external view returns (address);

    ///@notice return the max twap deviation
    ///@return int24 max twap deviation
    function maxTwapDeviation() external view returns (int24);

    ///@notice return the twap duration
    ///@return uint32 twap duration
    function twapDuration() external view returns (uint32);

    ///@notice return the address of Governance
    ///@return address of Governance
    function getModuleKeys() external view returns (bytes32[] memory);

    ///@notice adds a new whitelisted keeper
    ///@param _keeper address of the new keeper
    function addKeeperToWhitelist(address _keeper) external;

    ///@notice remove a whitelisted keeper
    ///@param _keeper address of the keeper to remove
    function removeKeeperFromWhitelist(address _keeper) external;

    ///@notice checks if the address is whitelisted as a keeper
    ///@param _keeper address to check
    ///@return bool true if the address is withelisted, false otherwise
    function whitelistedKeepers(address _keeper) external view returns (bool);

    function getModuleInfo(bytes32 _id)
        external
        view
        returns (
            address,
            bool,
            bytes32,
            bool
        );
}

// SPDX-License-Identifier: GPL-2.0

pragma solidity 0.7.6;
pragma abicoder v2;

interface IUniswapAddressHolder {
    ///@notice default getter for nonfungiblePositionManagerAddress
    ///@return address The address of the non fungible position manager
    function nonfungiblePositionManagerAddress() external view returns (address);

    ///@notice default getter for uniswapV3FactoryAddress
    ///@return address The address of the Uniswap V3 factory
    function uniswapV3FactoryAddress() external view returns (address);

    ///@notice default getter for swapRouterAddress
    ///@return address The address of the swap router
    function swapRouterAddress() external view returns (address);

    ///@notice Set the address of nonfungible position manager
    ///@param newAddress new address of nonfungible position manager
    function setNonFungibleAddress(address newAddress) external;

    ///@notice Set the address of the Uniswap V3 factory
    ///@param newAddress new address of the Uniswap V3 factory
    function setFactoryAddress(address newAddress) external;

    ///@notice Set the address of uniV3 swap router
    ///@param newAddress new address of univ3 swap router
    function setSwapRouterAddress(address newAddress) external;

    ///@notice Set the address of the registry
    ///@param newAddress The address of the registry
    function setRegistry(address newAddress) external;
}