/**
 *Submitted for verification at polygonscan.com on 2022-04-05
*/

// File: src/openzeppelin-contracts-release-v4.0/contracts/security/ReentrancyGuard.sol



pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor () {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// File: src/openzeppelin-contracts-release-v4.0/contracts/utils/cryptography/ECDSA.sol



pragma solidity ^0.8.0;

/**
 * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.
 *
 * These functions can be used to verify that a message was signed by the holder
 * of the private keys of a given address.
 */
library ECDSA {
    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature`. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     */
    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {
        // Check the signature length
        if (signature.length != 65) {
            revert("ECDSA: invalid signature length");
        }

        // Divide the signature in r, s and v variables
        bytes32 r;
        bytes32 s;
        uint8 v;

        // ecrecover takes the signature parameters, and the only way to get them
        // currently is to use assembly.
        // solhint-disable-next-line no-inline-assembly
        assembly {
            r := mload(add(signature, 0x20))
            s := mload(add(signature, 0x40))
            v := byte(0, mload(add(signature, 0x60)))
        }

        return recover(hash, v, r, s);
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `v`,
     * `r` and `s` signature fields separately.
     */
    function recover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) internal pure returns (address) {
        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
        // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most
        // signatures from current libraries generate a unique signature with an s-value in the lower half order.
        //
        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
        // these malleable signatures as well.
        require(uint256(s) <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0, "ECDSA: invalid signature 's' value");
        require(v == 27 || v == 28, "ECDSA: invalid signature 'v' value");

        // If the signature is valid (and not malleable), return the signer address
        address signer = ecrecover(hash, v, r, s);
        require(signer != address(0), "ECDSA: invalid signature");

        return signer;
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from a `hash`. This
     * produces hash corresponding to the one signed with the
     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]
     * JSON-RPC method as part of EIP-191.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {
        // 32 is the length in bytes of hash,
        // enforced by the type signature above
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }

    /**
     * @dev Returns an Ethereum Signed Typed Data, created from a
     * `domainSeparator` and a `structHash`. This produces hash corresponding
     * to the one signed with the
     * https://eips.ethereum.org/EIPS/eip-712[`eth_signTypedData`]
     * JSON-RPC method as part of EIP-712.
     *
     * See {recover}.
     */
    function toTypedDataHash(bytes32 domainSeparator, bytes32 structHash) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19\x01", domainSeparator, structHash));
    }
}

// File: src/openzeppelin-contracts-release-v4.0/contracts/utils/cryptography/draft-EIP712.sol



pragma solidity ^0.8.0;


/**
 * @dev https://eips.ethereum.org/EIPS/eip-712[EIP 712] is a standard for hashing and signing of typed structured data.
 *
 * The encoding specified in the EIP is very generic, and such a generic implementation in Solidity is not feasible,
 * thus this contract does not implement the encoding itself. Protocols need to implement the type-specific encoding
 * they need in their contracts using a combination of `abi.encode` and `keccak256`.
 *
 * This contract implements the EIP 712 domain separator ({_domainSeparatorV4}) that is used as part of the encoding
 * scheme, and the final step of the encoding to obtain the message digest that is then signed via ECDSA
 * ({_hashTypedDataV4}).
 *
 * The implementation of the domain separator was designed to be as efficient as possible while still properly updating
 * the chain id to protect against replay attacks on an eventual fork of the chain.
 *
 * NOTE: This contract implements the version of the encoding known as "v4", as implemented by the JSON RPC method
 * https://docs.metamask.io/guide/signing-data.html[`eth_signTypedDataV4` in MetaMask].
 *
 * _Available since v3.4._
 */
abstract contract EIP712 {
    /* solhint-disable var-name-mixedcase */
    // Cache the domain separator as an immutable value, but also store the chain id that it corresponds to, in order to
    // invalidate the cached domain separator if the chain id changes.
    bytes32 private immutable _CACHED_DOMAIN_SEPARATOR;
    uint256 private immutable _CACHED_CHAIN_ID;

    bytes32 private immutable _HASHED_NAME;
    bytes32 private immutable _HASHED_VERSION;
    bytes32 private immutable _TYPE_HASH;
    /* solhint-enable var-name-mixedcase */

    /**
     * @dev Initializes the domain separator and parameter caches.
     *
     * The meaning of `name` and `version` is specified in
     * https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator[EIP 712]:
     *
     * - `name`: the user readable name of the signing domain, i.e. the name of the DApp or the protocol.
     * - `version`: the current major version of the signing domain.
     *
     * NOTE: These parameters cannot be changed except through a xref:learn::upgrading-smart-contracts.adoc[smart
     * contract upgrade].
     */
    constructor(string memory name, string memory version) {
        bytes32 hashedName = keccak256(bytes(name));
        bytes32 hashedVersion = keccak256(bytes(version));
        bytes32 typeHash = keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)");
        _HASHED_NAME = hashedName;
        _HASHED_VERSION = hashedVersion;
        _CACHED_CHAIN_ID = block.chainid;
        _CACHED_DOMAIN_SEPARATOR = _buildDomainSeparator(typeHash, hashedName, hashedVersion);
        _TYPE_HASH = typeHash;
    }

    /**
     * @dev Returns the domain separator for the current chain.
     */
    function _domainSeparatorV4() internal view returns (bytes32) {
        if (block.chainid == _CACHED_CHAIN_ID) {
            return _CACHED_DOMAIN_SEPARATOR;
        } else {
            return _buildDomainSeparator(_TYPE_HASH, _HASHED_NAME, _HASHED_VERSION);
        }
    }

    function _buildDomainSeparator(bytes32 typeHash, bytes32 name, bytes32 version) private view returns (bytes32) {
        return keccak256(
            abi.encode(
                typeHash,
                name,
                version,
                block.chainid,
                address(this)
            )
        );
    }

    /**
     * @dev Given an already https://eips.ethereum.org/EIPS/eip-712#definition-of-hashstruct[hashed struct], this
     * function returns the hash of the fully encoded EIP712 message for this domain.
     *
     * This hash can be used together with {ECDSA-recover} to obtain the signer of a message. For example:
     *
     * ```solidity
     * bytes32 digest = _hashTypedDataV4(keccak256(abi.encode(
     *     keccak256("Mail(address to,string contents)"),
     *     mailTo,
     *     keccak256(bytes(mailContents))
     * )));
     * address signer = ECDSA.recover(digest, signature);
     * ```
     */
    function _hashTypedDataV4(bytes32 structHash) internal view virtual returns (bytes32) {
        return ECDSA.toTypedDataHash(_domainSeparatorV4(), structHash);
    }
}

// File: src/openzeppelin-contracts-release-v4.0/contracts/utils/Address.sol



pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// File: src/openzeppelin-contracts-release-v4.0/contracts/utils/Strings.sol



pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant alphabet = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = alphabet[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

}

// File: src/openzeppelin-contracts-release-v4.0/contracts/zetrix/NFTExchange.sol


pragma solidity ^0.8.4;






library ArrayUtils {

    function arrayEq(bytes memory a, bytes memory b)
        internal
        pure
        returns (bool)
    {
        bool success = true;

        assembly {
            let length := mload(a)

            // if lengths don't match the arrays are not equal
            switch eq(length, mload(b))
            case 1 {
                // cb is a circuit breaker in the for loop since there's
                //  no said feature for inline assembly loops
                // cb = 1 - don't breaker
                // cb = 0 - break
                let cb := 1

                let mc := add(a, 0x20)
                let end := add(mc, length)

                for {
                    let cc := add(b, 0x20)
                // the next line is the loop condition:
                // while(uint(mc < end) + cb == 2)
                } eq(add(lt(mc, end), cb), 2) {
                    mc := add(mc, 0x20)
                    cc := add(cc, 0x20)
                } {
                    // if any of these checks fails then arrays are not equal
                    if iszero(eq(mload(mc), mload(cc))) {
                        // unsuccess:
                        success := 0
                        cb := 0
                    }
                }
            }
            default {
                // unsuccess:
                success := 0
            }
        }

        return success;
    }

    function unsafeWriteAddress(uint index, address source)
        internal
        pure
        returns (uint)
    {
        uint conv = uint(uint160(source)) << 0x60;
        assembly {
            mstore(index, conv)
            index := add(index, 0x14)
        }
        return index;
    }

    function unsafeWriteUint(uint index, uint source)
        internal
        pure
        returns (uint)
    {
        assembly {
            mstore(index, source)
            index := add(index, 0x20)
        }
        return index;
    }

    function unsafeWriteUint8(uint index, uint8 source)
        internal
        pure
        returns (uint)
    {
        assembly {
            mstore8(index, source)
            index := add(index, 0x1)
        }
        return index;
    }

    function unsafeWriteBytes(uint index, bytes memory source)
        internal
        pure
        returns (uint)
    {
        if (source.length > 0) {
            assembly {
                let length := mload(source)
                let end := add(source, add(0x20, length))
                let arrIndex := add(source, 0x20)
                let tempIndex := index
                for { } eq(lt(arrIndex, end), 1) {
                    arrIndex := add(arrIndex, 0x20)
                    tempIndex := add(tempIndex, 0x20)
                } {
                    mstore(tempIndex, mload(arrIndex))
                }
                index := add(index, length)
            }
        }
        return index;
    }
}

interface I721{
    function safeTransferFrom(address from, address to, uint256 tokenId) external;
}

interface I1155{
    function safeTransferFrom(address from, address to, uint256 id, uint256 amount, bytes calldata data) external;
}

interface I20{
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
}

contract NFTExchange is EIP712, ReentrancyGuard{
    string public constant name = "NFT Exchange";
    string public constant version = "1.0";
    uint256 public constant INVERSE_BASIS_POINT = 10000;

    constructor() EIP712(name, version){}

    enum Side { Buy, Sell }
    enum NftType { NFT721, NFT1155 }
    enum OrderType { Maker, Taker }

    mapping(bytes32 => bool) public cancelledOrFinalized;

    event OrderCancelled (bytes32 indexed hash);
    event OrdersMatched (bytes32 buyHash, bytes32 sellHash, uint256 price);
    using ECDSA for bytes32;
    using Strings for uint256;
    using Address for address;

    struct Order {
        address exchange; 
        address ownerAddress;
        address targetAddress;
        address specAddress;
        uint256 fee;
        address feeRecipient;
        uint8 side;
        address paymentAddress;
        uint256 paymentPrice;
        address nftTokenAddress;
        uint8 nftTokenType;
        uint256 nftTokenID;
        uint256 nftTokenValue;
        bytes nftTokenData;
        uint256 listingTime;
        uint256 expirationTime;
        uint256 salt;
    }

    function atomicMatch(Order memory buy, bytes memory buySignature, Order memory sell, bytes memory sellSignature) public payable nonReentrant{
        bytes32 buyOrderHash = _checkSingleOrderValid(buy, buySignature);
        bytes32 sellOrderHash = _checkSingleOrderValid(sell, sellSignature);
        _checkMatchOrderValid(buy, buyOrderHash, sell, sellOrderHash);
        cancelledOrFinalized[buyOrderHash] = true;
        cancelledOrFinalized[sellOrderHash] = true;
        _transfer(buy, sell);

        emit OrdersMatched(buyOrderHash, sellOrderHash, buy.paymentPrice);
    }

    function _transfer(Order memory buy, Order memory sell) internal {
        uint256 feePrice = buy.fee * buy.paymentPrice / INVERSE_BASIS_POINT;
        uint256 payPrice = buy.paymentPrice - feePrice;
        
        if(buy.paymentAddress == address(0)){
            require(msg.value == buy.paymentPrice, "Payment price must equal sender pay coin amount.");
            payable(sell.ownerAddress).transfer(payPrice);
            if(feePrice > 0){
                payable(sell.feeRecipient).transfer(feePrice);
            }
        }
        else{
            require(msg.value == 0, "ERC20 token ,pay value must be 0.");
            bool result = false;
            I20 ierc20 = I20(buy.paymentAddress);
            result = ierc20.transferFrom(buy.ownerAddress, sell.ownerAddress, payPrice);
            require(result, "Failed to transfer from erc20 with pay price.");
            if(feePrice > 0){
                result = ierc20.transferFrom(buy.ownerAddress, buy.feeRecipient, feePrice);
                require(result, "Failed to transfer from erc20 with fee price.");
            }
        }
        
        //transfer nft
        if(NftType(sell.nftTokenType) == NftType.NFT721){
            I721 ierc721 = I721(buy.nftTokenAddress);
            ierc721.safeTransferFrom(sell.ownerAddress, buy.ownerAddress, sell.nftTokenID);
        }

        if(NftType(sell.nftTokenType) == NftType.NFT1155){
            I1155 ierc1155 = I1155(buy.nftTokenAddress);
            //require(sell.nftTokenID == 38573576508781952517264217419122654634213705236420586713731419821284165419009, sell.nftTokenID.toString());
            //require(sell.nftTokenValue == 1, sell.nftTokenValue.toString());
            ierc1155.safeTransferFrom(sell.ownerAddress, buy.ownerAddress, sell.nftTokenID, sell.nftTokenValue, sell.nftTokenData);
        }
        
    }

    function _addressToString(address _address) internal pure returns(string memory) {
        bytes32 _bytes = bytes32(uint256(uint160(_address)));
        bytes memory HEX = "0123456789abcdef";
        bytes memory _string = new bytes(42);
        _string[0] = '0';
        _string[1] = 'x';
        for(uint i = 0; i < 20; i++) {
            _string[2+i*2] = HEX[uint8(_bytes[i + 12] >> 4)];
            _string[3+i*2] = HEX[uint8(_bytes[i + 12] & 0x0f)];
        }
        return string(_string);
    }

    function _checkSingleOrderValid(Order memory order, bytes memory signature) internal view returns (bytes32) {
        OrderType order_type = OrderType.Maker;
        bytes32 orderHash = hashOrder(order);
        require(order.exchange == address(this), "Exchange must be equal this contract.");
        require(order.ownerAddress != address(0), "Ownder address must not be 0.");
        require(order.nftTokenAddress != address(0), "Nft token address must not be 0.");
        require(order.nftTokenAddress.isContract() == true, "Nft token address must be contract.");
        require(order.feeRecipient != address(0), "Fee recipient address must not be 0.");
        require(order.fee > 0, "Order fee must greater than 0.");
        require(order.nftTokenID > 0, "Order token id must greater than 0.");
        require(order.nftTokenValue > 0, "Order token value must greater than 0.");
        if(order.paymentAddress != address(0)){
            require(order.paymentAddress.isContract() == true, "Payment address must be contract.");
        }

        if(order.targetAddress != address(0)){
            order_type = OrderType.Taker;
        }

        require(NftType(order.nftTokenType) == NftType.NFT1155 || NftType(order.nftTokenType) == NftType.NFT721, "Nft type must be 1155 or 721.");

        require(block.timestamp >= order.listingTime, "Order listing time must less than or equal to block timestamp.");
        if(order.expirationTime > 0){
            require(order.expirationTime >= block.timestamp, "Order expiration time must lager than or equal to block timestamp.");
        }
        
        if(signature.length > 0){
            require(verify(order, signature, order.ownerAddress), "Signature error.");
        }
        else{
            require(order_type == OrderType.Taker, "Sig is null, need order is taker.");
            require(msg.sender == order.ownerAddress, "Sig is null, need order address equal sender.");
        }
        
        return orderHash;
    }

    function _checkMatchOrderValid(Order memory buy, bytes32 buyOrderHash, Order memory sell, bytes32 sellOrderHash) internal view{
        require(cancelledOrFinalized[buyOrderHash] == false, "CancelledOrFinalized buy order.");
        require(cancelledOrFinalized[sellOrderHash] == false, "CancelledOrFinalized sell order.");
        require(Side(buy.side) == Side.Buy, "Buy order size error.");
        require(Side(sell.side) == Side.Sell, "Sell order side error.");
        require(buy.fee == sell.fee, "Fee must equal.");
        require(buy.feeRecipient == sell.feeRecipient, "Fee recipient must equal.");
        require(buy.paymentAddress == sell.paymentAddress, "Payment address must equal.");
        require(buy.paymentPrice >= sell.paymentPrice, "Buy payment price must equal or greater than sell.");
        require(buy.nftTokenType == sell.nftTokenType, "Nft token type must equal.");
        require(buy.nftTokenAddress == sell.nftTokenAddress, "Nft token address must equal.");
        require(buy.nftTokenID == sell.nftTokenID, "Nft token ID must equal.");
        require(buy.nftTokenValue == sell.nftTokenValue, "Nft token value must equal.");
        require(ArrayUtils.arrayEq(buy.nftTokenData, sell.nftTokenData), "Nft token data must equal.");
        OrderType buyOrderType = (buy.targetAddress == address(0)) ? OrderType.Maker : OrderType.Taker;
        OrderType sellOrderType = (sell.targetAddress == address(0)) ? OrderType.Maker : OrderType.Taker;
        require(buyOrderType != sellOrderType, "Order type must not same.");
        if(buyOrderType == OrderType.Taker){
            require(buy.targetAddress == sell.ownerAddress, "Buy order must taker.");
        }
        if(sellOrderType == OrderType.Taker){
            require(sell.targetAddress == buy.ownerAddress, "Sell order must taker.");
        }
        if(buy.specAddress != address(0)){
            require(buy.specAddress == sell.ownerAddress, "Buy spec address must equal.");
        }

        if(sell.specAddress != address(0)){
            require(sell.specAddress == buy.ownerAddress, "Sell spec address must equal.");
        }
    }

    function _sizeOf(Order memory order) internal pure returns (uint){
        return ((0x14 * 7) + (0x20 * 7) + 2 + order.nftTokenData.length);
    }

    function encodeOrder(Order memory order) public pure returns (bytes memory){
        /* Unfortunately abi.encodePacked doesn't work here, stack size constraints. */
        uint size = _sizeOf(order);
        bytes memory array = new bytes(size);
        uint index;
        assembly {
            index := add(array, 0x20)
        }
        index = ArrayUtils.unsafeWriteAddress(index, order.exchange);
        index = ArrayUtils.unsafeWriteAddress(index, order.ownerAddress);
        index = ArrayUtils.unsafeWriteAddress(index, order.targetAddress);
        index = ArrayUtils.unsafeWriteAddress(index, order.specAddress);
        index = ArrayUtils.unsafeWriteUint(index, order.fee);
        index = ArrayUtils.unsafeWriteAddress(index, order.feeRecipient);
        index = ArrayUtils.unsafeWriteUint8(index, uint8(order.side));
        index = ArrayUtils.unsafeWriteAddress(index, order.paymentAddress);
        index = ArrayUtils.unsafeWriteUint(index, order.paymentPrice);
        index = ArrayUtils.unsafeWriteAddress(index, order.nftTokenAddress);
        index = ArrayUtils.unsafeWriteUint8(index, uint8(order.nftTokenType));
        index = ArrayUtils.unsafeWriteUint(index, order.nftTokenID);
        index = ArrayUtils.unsafeWriteUint(index, order.nftTokenValue);
        index = ArrayUtils.unsafeWriteBytes(index, order.nftTokenData);
        index = ArrayUtils.unsafeWriteUint(index, order.listingTime);
        index = ArrayUtils.unsafeWriteUint(index, order.expirationTime);
        index = ArrayUtils.unsafeWriteUint(index, order.salt);

        return array;
    }

    function hashOrder(Order memory order) public pure returns (bytes32 hash){
        return keccak256(abi.encodePacked(encodeOrder(order)));
    }

    function verify(Order memory order, bytes memory signature, address expectAddress) public pure returns (bool) {
        bytes32 dataHash = hashOrder(order).toEthSignedMessageHash();
        address recoverAddress = ECDSA.recover(dataHash, signature);
        if (recoverAddress == expectAddress) {
            return true;
        }

        //require(recoverAddress == expectAddress, _addressToString(recoverAddress));
        return false;
    }
    
    function cancelOrder(Order calldata order, bytes calldata signature) public{
        bytes32 hash = _checkSingleOrderValid(order, signature);
        require(cancelledOrFinalized[hash] == false, "Order has existed.");
        require(msg.sender == order.ownerAddress, "Must be owner address.");
        cancelledOrFinalized[hash] = true;
        emit OrderCancelled(hash);
    }
}