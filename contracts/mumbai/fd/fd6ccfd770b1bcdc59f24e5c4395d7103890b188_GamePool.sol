/**
 *Submitted for verification at polygonscan.com on 2022-06-04
*/

// Sources flattened with hardhat v2.9.7 https://hardhat.org

// File contracts/interfaces/IGamePool.sol

// SPDX-License-Identifier: MIT
pragma solidity 0.8.14;

struct forcedWithdrawal {
	uint timestamp;
	uint amount;
}

struct balanceChange {
	address token;
	address[] accounts;
	int[] changes;
}

struct withdrawal {
	address account;
	address token;
	uint amount;
}

interface IGamePool {
	function prepareForcedWithdrawal(address token) external;
	function forceWithdraw(address token) external;
	function withdraw(address token) external;
	function withdrawBatch(address[] calldata tokens) external;
	function setOperator(address operator, bool isOperator) external;
	function withdrawalTime() external view returns (uint withdrawalTime);

	function operators(address account) external view returns (bool isOperator);
	function balanceOf(address token, address account) external view returns (uint balance);
	function readyWithdrawal(address token, address account) external view returns (uint readyWithdrawal);
	function forcedWithdrawalAmount(address token, address account) external view returns (uint amount);
	function forcedWithdrawalTime(address token, address account) external view returns (uint startTime);

	function fetch(
		uint _nonce,
		withdrawal[] calldata withdrawals,
		balanceChange[] calldata balanceChanges
	) external;

	event ForcedWithdrawalRequested(address indexed account, address indexed token, uint value);
	event ForcedWithdrawalPerformed(address indexed account, address indexed token, uint value);
	event WithdrawalAllowed(address indexed account, address indexed token, uint value);
	event Withdrawal(address indexed account, address indexed token, uint value);
	event FundsDeposited(address indexed who, address indexed token, uint value);
	event OperatorSet(address indexed operator, bool isOperator);
	event Fetch(
		uint indexed _nonce,
		withdrawal[] withdrawals,
		balanceChange[] balanceChanges
	);
}

// File @openzeppelin/contracts-upgradeable/utils/[email protected]

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// File @openzeppelin/contracts-upgradeable/proxy/utils/[email protected]

pragma solidity ^0.8.2;

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be
 * reused. This mechanism prevents re-execution of each "step" but allows the creation of new initialization steps in
 * case an upgrade adds a module that needs to be initialized.
 *
 * For example:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * contract MyToken is ERC20Upgradeable {
 *     function initialize() initializer public {
 *         __ERC20_init("MyToken", "MTK");
 *     }
 * }
 * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {
 *     function initializeV2() reinitializer(2) public {
 *         __ERC20Permit_init("MyToken");
 *     }
 * }
 * ```
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke
 * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() {
 *     _disableInitializers();
 * }
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     * @custom:oz-retyped-from bool
     */
    uint8 private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Triggered when the contract has been initialized or reinitialized.
     */
    event Initialized(uint8 version);

    /**
     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,
     * `onlyInitializing` functions can be used to initialize parent contracts. Equivalent to `reinitializer(1)`.
     */
    modifier initializer() {
        bool isTopLevelCall = _setInitializedVersion(1);
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(1);
        }
    }

    /**
     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the
     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be
     * used to initialize parent contracts.
     *
     * `initializer` is equivalent to `reinitializer(1)`, so a reinitializer may be used after the original
     * initialization step. This is essential to configure modules that are added through upgrades and that require
     * initialization.
     *
     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in
     * a contract, executing them in the right order is up to the developer or operator.
     */
    modifier reinitializer(uint8 version) {
        bool isTopLevelCall = _setInitializedVersion(version);
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(version);
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} and {reinitializer} modifiers, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    /**
     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.
     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized
     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called
     * through proxies.
     */
    function _disableInitializers() internal virtual {
        _setInitializedVersion(type(uint8).max);
    }

    function _setInitializedVersion(uint8 version) private returns (bool) {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, and for the lowest level
        // of initializers, because in other contexts the contract may have been reentered.
        if (_initializing) {
            require(
                version == 1 && !AddressUpgradeable.isContract(address(this)),
                "Initializable: contract is already initialized"
            );
            return false;
        } else {
            require(_initialized < version, "Initializable: contract is already initialized");
            _initialized = version;
            return true;
        }
    }
}

// File @openzeppelin/contracts-upgradeable/security/[email protected]

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuardUpgradeable is Initializable {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    function __ReentrancyGuard_init() internal onlyInitializing {
        __ReentrancyGuard_init_unchained();
    }

    function __ReentrancyGuard_init_unchained() internal onlyInitializing {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}

// File @openzeppelin/contracts-upgradeable/utils/[email protected]

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[50] private __gap;
}

// File @openzeppelin/contracts-upgradeable/access/[email protected]

pragma solidity ^0.8.0;

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal onlyInitializing {
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal onlyInitializing {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}

// File @openzeppelin/contracts-upgradeable/token/ERC20/[email protected]

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// File @openzeppelin/contracts-upgradeable/token/ERC20/utils/[email protected]

pragma solidity ^0.8.0;


/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20Upgradeable {
    using AddressUpgradeable for address;

    function safeTransfer(
        IERC20Upgradeable token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20Upgradeable token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20Upgradeable token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20Upgradeable token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20Upgradeable token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20Upgradeable token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// File @openzeppelin/contracts-upgradeable/utils/math/[email protected]

pragma solidity ^0.8.0;

/**
 * @dev Standard math utilities missing in the Solidity language.
 */
library MathUpgradeable {
    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a >= b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Returns the average of two numbers. The result is rounded towards
     * zero.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow.
        return (a & b) + (a ^ b) / 2;
    }

    /**
     * @dev Returns the ceiling of the division of two numbers.
     *
     * This differs from standard division with `/` in that it rounds up instead
     * of rounding down.
     */
    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b - 1) / b can overflow on addition, so we distribute.
        return a / b + (a % b == 0 ? 0 : 1);
    }
}

// File contracts/GamePool.sol

pragma solidity 0.8.14;

// this contract is not compatible with fee-on-transfer tokens!
/**
 * @title Game Pool
 * @author Czar102
 * @notice The contract managing tokens that gamers are playing
 * with. Operators can transfer tokens internally to winners from
 * losers. There is also an option of withdrawal if VXG goes down.
 * This is allowed after a cooldown not to allow frontrunning by
 * losers.
 * @dev Note that it's possible to force withdraw all but 1 token
 * not to force operators to pay additional gas in case the
 * tokens used in forced withdrawal need to be transferred.
 * @dev This contract is not compatible with fee on transfer tokens.
 * This is a design choice to decrease the gas consumption.
 */
contract GamePool is ReentrancyGuardUpgradeable, OwnableUpgradeable, IGamePool {
	using SafeERC20Upgradeable for IERC20Upgradeable;
	uint public version;
	uint public nonce;

	uint public withdrawalTime;

	mapping (address => bool) public operators; // todo implement OZ role-based system
	mapping (address => mapping (address => uint)) public balanceOf; // token => user => balance
	mapping (address => mapping (address => forcedWithdrawal)) private forcedWithdrawalOf; // token => user => withdrawal
	mapping (address => mapping (address => uint)) public readyWithdrawal; // token => user => withdrawal

	/**
	 * @notice Used to view current amount that is a subject to
	 * forced withdrawals by an account
	 * @param token the token of the forced withdrawal
	 * @param account the account that the forced withdrawal concerns
	 * @return amount waiting for forced withdrawal
	 */
	function forcedWithdrawalAmount(address token, address account) external view returns (uint amount) {
		return forcedWithdrawalOf[token][account].amount;
	}

	/**
	 * @notice Used to view the time of forced withdrawal creation.
	 * @dev This is used to derive the minimal timestamp a forced
	 * withdrawal can be executed.
	 * @param token the token of the forced withdrawal
	 * @param account the account that the forced withdrawal concerns
	 * @return startTime the timestamp when the forced withdrawal started
	 */
	function forcedWithdrawalTime(address token, address account) external view returns (uint startTime) {
		return forcedWithdrawalOf[token][account].timestamp;
	}

	/**
	 * @dev initializer of the contract
	 * @param _withdrawalTime the time that is needed for a forced
	 * withdrawal to be considered valid (not frontrunning a transfer
	 * from an address)
	 */
	function initialize(uint _withdrawalTime) public initializer {
		require(_withdrawalTime != 0, "GamePool: Time cannot be 0");
		withdrawalTime = _withdrawalTime;

		__ReentrancyGuard_init();
		__Ownable_init();
	}

	/**
	 * @notice Used to deposit funds to the contract. Note that
	 * tokens must be approved before. Transfers must be done from
	 * the sender and their internal balance is increased.
	 * @param token the token to deposit
	 * @param value the number of tokens to deposit in token wei
	 */
	function depositFunds(address token, uint value) external {
		IERC20Upgradeable(token).safeTransferFrom(msg.sender, address(this), value);
		balanceOf[token][msg.sender] += value;

		emit FundsDeposited(msg.sender, token, value);
	}

	/**
	 * @notice A function to prepare forced withdrawal for msg.sender.
	 * @dev All tokens but 1 will be prepared for forced withdrawal
	 * not to force operators to increase their gas needs in case of a
	 * malicious forced withdrawal (frontrunning transfer from an
	 * account)
	 * @dev If there already is an active forced withdrawal, the time
	 * will be overwritten, resetting the clock for that forced
	 * withdrawal.
	 * @param token the token that the forced withdrawal concerns
	 */
	function prepareForcedWithdrawal(address token) external {
		uint _amount = balanceOf[token][msg.sender];
		forcedWithdrawal storage tokenWithdrawal = forcedWithdrawalOf[token][msg.sender];
		// the zeroness of balanceOf[token][addr] doesn't change during cancellation
		require(_amount > 1, "GamePool: Not enough funds!");
		_amount = _amount + tokenWithdrawal.amount - 1;
		balanceOf[token][msg.sender] = 1;

		tokenWithdrawal.timestamp = block.timestamp;
		tokenWithdrawal.amount += _amount;

		emit ForcedWithdrawalRequested(msg.sender, token, _amount);
	}

	/**
	 * @notice A function to execute a forced withdrawal with the
	 * full prepared amount.
	 * @dev It is required to wait withdrawalTime after the last
	 * modification to the withdrawal.
	 * @param token the token that the forced withdrawal concerns
	 */
	function forceWithdraw(address token) external {
		require(
			block.timestamp - forcedWithdrawalOf[token][msg.sender].timestamp >= withdrawalTime,
			"GamePool: Can't withdraw your tokens yet!"
		);
		uint _amount = forcedWithdrawalOf[token][msg.sender].amount;

		delete forcedWithdrawalOf[token][msg.sender];

		IERC20Upgradeable(token).safeTransfer(msg.sender, _amount);

		emit ForcedWithdrawalPerformed(msg.sender, token, _amount);
	}

	/**
	 * @notice Used to withdraw funds that a user is allowed by
	 * an operator. Withdraws all tokens allowed for withdrawal.
	 * @param token the token that the forced withdrawal concerns
	 */
	function withdraw(address token) public {
		uint amount = readyWithdrawal[token][msg.sender];
		require(amount > 0, "GamePool: amount is 0");
		readyWithdrawal[token][msg.sender] = 0;
		IERC20Upgradeable(token).safeTransfer(msg.sender, amount);
	}

	/**
	 * @notice Used to withdraw funds that a user is allowed by
	 * an operator. Withdraws all tokens allowed for withdrawal.
	 * @param tokens a list of tokens that the user wants to
	 * withdraw
	 */
	function withdrawBatch(address[] calldata tokens) external {
		for (uint i; i < tokens.length; i++) {
			withdraw(tokens[i]);
		}
	}

	/**
	 * @notice Sets whether an address is an operator
	 * @dev only callable by the owner
	 * @param operator the address that for which the operator role
	 * is granted or revoked
	 * @param isOperator the value whether operator should be one
	 */
	function setOperator(address operator, bool isOperator) external onlyOwner {
		operators[operator] = isOperator;
		emit OperatorSet(operator, isOperator);
	}

	/**
	 * @notice Sets time needed to consider forced withdrawal not
	 * frontrunning on a lost game
	 * @dev only callable by the owner
	 * @param time the time it will take to grant permission to
	 * force withdraw
	 */
	function setWithdrawalTime(uint time) external onlyOwner {
		require(time != 0, "GamePool: Time cannot be 0");
		withdrawalTime = time;
	}

	/**
	 * @notice Used to fetch balances, that is - perform internal
	 * transfers. Also allows for granting permission to withdraw
	 * immediately.
	 * @dev The nonce is incremented and must be equal to the current
	 * nonce.
	 * @dev Only callable by operators.
	 * @param _nonce the current nonce of the contract
	 * @param withdrawals a list of withdrawals that are allowed to
	 * be done immediately
	 * @param balanceChanges the result of all transfers. Some
	 * accounts might have transferred many times and it is redundant
	 * to report every transfer on chain.
	 */
	function fetch(
		uint _nonce,
		withdrawal[] calldata withdrawals,
		balanceChange[] calldata balanceChanges
	) nonReentrant external {
		require(operators[msg.sender], "GamePool: Only VXG operator can call this function!");
		require(_nonce == nonce++, "GamePool: wrong nonce");

		for (uint i = 0; i < withdrawals.length; i++) {
			address account = withdrawals[i].account;
			address token = withdrawals[i].token;
			uint amount = withdrawals[i].amount;

			// If someone withdrew, silently allow lower withdrawal
			uint balance = balanceOf[token][account];
			uint finalAmount = MathUpgradeable.min(balance, amount);

			balanceOf[token][account] = balance - finalAmount;
			readyWithdrawal[token][account] += finalAmount;

			emit WithdrawalAllowed(account, token, finalAmount);
		}


		for (uint i; i < balanceChanges.length; i++) {
			require(
				balanceChanges[i].accounts.length == balanceChanges[i].changes.length,
				"GamePool: array lengths must be aligned"
			);
			int changeSum;
			address token = balanceChanges[i].token;

			for (uint j; j < balanceChanges[i].accounts.length; j++) {
				address account = balanceChanges[i].accounts[j];
				int change = balanceChanges[i].changes[j];
				
				changeSum += change;

				uint balance = balanceOf[token][account];
				if (change + int(balance) < 0) {
					uint forcedValue = forcedWithdrawalOf[token][account].amount;
					require(
						change + int(balance) + int(forcedValue) >= 0,
						"GamePool: Not enough funds to take!"
					);
					delete forcedWithdrawalOf[token][account];
					balance += forcedValue;
				}
				balanceOf[token][account] = uint(int(balance) + change);
			}

			require(
				changeSum == 0,
				"GamePool: sum of changes must be 0"
			);
		}

		emit Fetch(nonce, withdrawals, balanceChanges);
	}
}