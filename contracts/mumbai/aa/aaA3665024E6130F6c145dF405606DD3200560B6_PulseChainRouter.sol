/**
 *Submitted for verification at polygonscan.com on 2021-12-22
*/

/**
 *Submitted for verification at BscScan.com on 2021-10-26
*/

/**
 *Submitted for verification at BscScan.com on 2021-10-20
*/

/**
 *Submitted for verification at BscScan.com on 2021-09-17
*/

pragma solidity >=0.8.0;

// helper mPULSEods for interacting with ERC20 tokens and sending PULSE that do not consistently return true/false
library TransferHelper {
    function safeApprove(
        address token,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('approve(address,uint256)')));
        (bool success, bytes memory data) = token.call(
            abi.encodeWithSelector(0x095ea7b3, to, value)
        );
        require(
            success && (data.length == 0 || abi.decode(data, (bool))),
            "TransferHelper: APPROVE_FAILED"
        );
    }

    function safeTransfer(
        address token,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(
            abi.encodeWithSelector(0xa9059cbb, to, value)
        );
        require(
            success && (data.length == 0 || abi.decode(data, (bool))),
            "TransferHelper: TRANSFER_FAILED"
        );
    }

    function safeTransferFrom(
        address token,
        address from,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(
            abi.encodeWithSelector(0x23b872dd, from, to, value)
        );
        require(
            success && (data.length == 0 || abi.decode(data, (bool))),
            "TransferHelper: TRANSFER_FROM_FAILED"
        );
    }

    function safeTransferPULSE(address to, uint256 value) internal {
        (bool success, ) = to.call{value: value}(new bytes(0));
        require(success, "TransferHelper: PULSE_TRANSFER_FAILED");
    }
}

// File: contracts\interfaces\IPulseChainRouter01.sol

interface IPulseChainRouter01 {
    function factory() external returns (address);

    function WPULSE() external returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint256 amountADesired,
        uint256 amountBDesired,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    )
        external
        returns (
            uint256 amountA,
            uint256 amountB,
            uint256 liquidity
        );

    function addLiquidityPULSE(
        address token,
        uint256 amountTokenDesired,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline
    )
        external
        payable
        returns (
            uint256 amountToken,
            uint256 amountPULSE,
            uint256 liquidity
        );

    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountA, uint256 amountB);

    function removeLiquidityPULSE(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountToken, uint256 amountPULSE);

    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountA, uint256 amountB);

    function removeLiquidityPULSEWithPermit(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountToken, uint256 amountPULSE);

    function swapExactTokensForTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapTokensForExactTokens(
        uint256 amountOut,
        uint256 amountInMax,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapExactPULSEForTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable returns (uint256[] memory amounts);

    function swapTokensForExactPULSE(
        uint256 amountOut,
        uint256 amountInMax,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapExactTokensForPULSE(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapPULSEForExactTokens(
        uint256 amountOut,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable returns (uint256[] memory amounts);

    function quote(
        uint256 amountA,
        uint256 reserveA,
        uint256 reserveB
    ) external pure returns (uint256 amountB);

    function getAmountOut(
        uint256 amountIn,
        uint256 reserveIn,
        uint256 reserveOut,
        uint256 swapFee
    ) external pure returns (uint256 amountOut);

    function getAmountIn(
        uint256 amountOut,
        uint256 reserveIn,
        uint256 reserveOut,
        uint256 swapFee
    ) external pure returns (uint256 amountIn);

    function getAmountsOut(uint256 amountIn, address[] calldata path)
        external
        view
        returns (uint256[] memory amounts);

    function getAmountsIn(uint256 amountOut, address[] calldata path)
        external
        view
        returns (uint256[] memory amounts);
}

// File: contracts\interfaces\IPulseChainRouter02.sol

interface IPulseChainRouter02 is IPulseChainRouter01 {
    function removeLiquidityPULSESupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountPULSE);

    function removeLiquidityPULSEWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountPULSE);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;

    function swapExactPULSEForTokensSupportingFeeOnTransferTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable;

    function swapExactTokensForPULSESupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;
}

// File: contracts\interfaces\IPulseChainFactory.sol

interface IPulseChainFactory {
    event PairCreated(
        address indexed token0,
        address indexed token1,
        address pair,
        uint256
    );

    function feeTo() external view returns (address);

    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB)
        external
        view
        returns (address pair);

    function allPairs(uint256) external view returns (address pair);

    function allPairsLength() external view returns (uint256);

    function createPair(address tokenA, address tokenB)
        external
        returns (address pair);

    function setFeeTo(address) external;

    function setFeeToSetter(address) external;

    function setDevFee(address pair, uint8 _devFee) external;

    function setPercent(
        address _pair,
        uint256 _ndev,
        uint256 _ddev
    ) external;

    function setSwapFee(address pair, uint32 swapFee) external;

    function INIT_CODE_PAIR_HASH() external view returns (bytes32);
}

// File: contracts\libraries\SafeMath.sol

// a library for performing overflow-safe math, courtesy of DappHub (https://github.com/dapphub/ds-math)

// library SafeMath {
//     function add(uint x, uint y) internal pure returns (uint z) {
//         require((z = x + y) >= x, 'ds-math-add-overflow');
//     }

//     function sub(uint x, uint y) internal pure returns (uint z) {
//         require((z = x - y) <= x, 'ds-math-sub-underflow');
//     }

//     function mul(uint x, uint y) internal pure returns (uint z) {
//         require(y == 0 || (z = x * y) / y == x, 'ds-math-mul-overflow');
//     }
// }

// File: contracts\interfaces\IPulseChainPair.sol

interface IPulseChainPair {
    event Approval(address indexed spender, uint256 value);
    event Transfer(address indexed from, address indexed to, uint256 value);

    function name() external pure returns (string memory);

    function symbol() external pure returns (string memory);

    function decimals() external pure returns (uint8);

    function totalSupply() external view returns (uint256);

    function balanceOf(address owner) external view returns (uint256);

    function allowance(address owner, address spender)
        external
        view
        returns (uint256);

    function approve(address spender, uint256 value) external returns (bool);

    function transfer(address to, uint256 value) external returns (bool);

    function transferFrom(
        address from,
        address to,
        uint256 value
    ) external returns (bool);

    function DOMAIN_SEPARATOR() external view returns (bytes32);

    function PERMIT_TYPEHASH() external pure returns (bytes32);

    function nonces(address owner) external view returns (uint256);

    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    event Mint(address indexed sender, uint256 amount0, uint256 amount1) ; 
    event Burn(
        address indexed sender,
        uint256 amount0,
        uint256 amount1,
        address indexed to
    );
    event Swap(
        address indexed sender,
        uint256 amount0In,
        uint256 amount1In,
        uint256 amount0Out,
        uint256 amount1Out,
        address indexed to
    );
    event Sync(uint112 reserve0, uint112 reserve1);

    function MINIMUM_LIQUIDITY() external pure returns (uint256);

    function factory() external view returns (address);

    function token0() external view returns (address);

    function token1() external view returns (address);

    function getReserves()
        external
        view
        returns (
            uint112 reserve0,
            uint112 reserve1,
            uint32 blockTimestampLast
        );

    function price0CumulativeLast() external view returns (uint256);

    function price1CumulativeLast() external view returns (uint256);

    function kLast() external view returns (uint256);

    function nDev() external view returns (uint256);

    function dDev() external view returns (uint256);

    function swapFee() external view returns (uint32);

    function devFee() external view returns (uint32);

    function mint(address to) external returns (uint256 liquidity,uint amount0,uint amount1);

    function burn(address to)
        external
        returns (uint256 amount0, uint256 amount1);

    function swap(
        uint256 amount0Out,
        uint256 amount1Out,
        address to,
        bytes calldata data
    ) external returns(uint amount0In,uint amount1In);

    function skim(address to) external;

    function sync() external;

    function initialize(address, address) external;

    function setSwapFee(uint32) external;

    function setPercent(uint256, uint256) external;

    function setDevFee(uint32) external;
}

// File: contracts\libraries\PulseChainLibrary.sol

library PulseChainLibrary {
    //using SafeMath for uint256;

    // returns sorted token addresses, used to handle return values from pairs sorted in this order
    function sortTokens(address tokenA, address tokenB)
        internal
        pure
        returns (address token0, address token1)
    {
        require(tokenA != tokenB, "PulseChainLibrary: IDENTICAL_ADDRESSES");
        (token0, token1) = tokenA < tokenB
            ? (tokenA, tokenB)
            : (tokenB, tokenA);
        require(token0 != address(0), "PulseChainLibrary: ZERO_ADDRESS");
    }

    // calculates the CREATE2 address for a pair without making any external calls
    function pairFor(
        address factory,
        address tokenA,
        address tokenB
    ) internal pure returns (address pair) {
        (address token0, address token1) = sortTokens(tokenA, tokenB);
        pair = address(
            uint160(
                uint256(
                    keccak256(
                        abi.encodePacked(
                            hex"ff",
                            factory,
                            keccak256(abi.encodePacked(token0, token1)),
                            hex"43e2e566c44a11d8ca02ffcdffa90ee5a0ad2b51ccb47a151fcfbf9bfc3dde3d" // init code hash
                        )
                    )
                )
            )
        );
    }

    function getSwapFee(
        address factory,
        address tokenA,
        address tokenB
    ) internal view returns (uint256 swapFee) {
        swapFee = IPulseChainPair(pairFor(factory, tokenA, tokenB)).swapFee();
    }

    // fetches and sorts the reserves for a pair
    function getReserves(
        address factory,
        address tokenA,
        address tokenB
    ) internal view returns (uint256 reserveA, uint256 reserveB) {
        (address token0, ) = sortTokens(tokenA, tokenB);
        pairFor(factory, tokenA, tokenB);
        (uint256 reserve0, uint256 reserve1, ) = IPulseChainPair(
            pairFor(factory, tokenA, tokenB)
        ).getReserves();
        (reserveA, reserveB) = tokenA == token0
            ? (reserve0, reserve1)
            : (reserve1, reserve0);
    }

    // given some amount of an asset and pair reserves, returns an equivalent amount of the other asset
    function quote(
        uint256 amountA,
        uint256 reserveA,
        uint256 reserveB
    ) internal pure returns (uint256 amountB) {
        require(amountA > 0, "PulseChainLibrary: INSUFFICIENT_AMOUNT");
        require(
            reserveA > 0 && reserveB > 0,
            "PulseChainLibrary: INSUFFICIENT_LIQUIDITY"
        );
        amountB = (amountA * (reserveB)) / reserveA;
    }

    // given an input amount of an asset and pair reserves, returns the maximum output amount of the other asset
    function getAmountOut(
        uint256 amountIn,
        uint256 reserveIn,
        uint256 reserveOut,
        uint256 swapFee
    ) internal pure returns (uint256 amountOut) {
        require(amountIn > 0, "PulseChainLibrary: INSUFFICIENT_INPUT_AMOUNT");
        require(
            reserveIn > 0 && reserveOut > 0,
            "PulseChainLibrary: INSUFFICIENT_LIQUIDITY"
        );
        uint256 amountInWithFee = amountIn * (uint256(10000) - (swapFee));
        uint256 numerator = amountInWithFee * (reserveOut);
        uint256 denominator = (reserveIn * (10000)) + (amountInWithFee);
        amountOut = numerator / denominator;
    }

    // given an output amount of an asset and pair reserves, returns a required input amount of the other asset
    function getAmountIn(
        uint256 amountOut,
        uint256 reserveIn,
        uint256 reserveOut,
        uint256 swapFee
    ) internal pure returns (uint256 amountIn) {
        require(amountOut > 0, "PulseChainLibrary: INSUFFICIENT_OUTPUT_AMOUNT");
        require(
            reserveIn > 0 && reserveOut > 0,
            "PulseChainLibrary: INSUFFICIENT_LIQUIDITY"
        );
        uint256 numerator = (reserveIn * (amountOut)) * (10000);
        uint256 denominator = (reserveOut - (amountOut)) *
            (uint256(10000) - (swapFee));
        amountIn = (numerator / denominator) + (1);
    }

    // performs chained getAmountOut calculations on any number of pairs
    function getAmountsOut(
        address factory,
        uint256 amountIn,
        address[] memory path
    ) internal view returns (uint256[] memory amounts) {
        require(path.length >= 2, "PulseChainLibrary: INVALID_PATH");
        amounts = new uint256[](path.length);
        amounts[0] = amountIn;
        for (uint256 i; i < path.length - 1; i++) {
            (uint256 reserveIn, uint256 reserveOut) = getReserves(
                factory,
                path[i],
                path[i + 1]
            );
            amounts[i + 1] = getAmountOut(
                amounts[i],
                reserveIn,
                reserveOut,
                getSwapFee(factory, path[i], path[i + 1])
            );
        }
    }

    // performs chained getAmountIn calculations on any number of pairs
    function getAmountsIn(
        address factory,
        uint256 amountOut,
        address[] memory path
    ) internal view returns (uint256[] memory amounts) {
        require(path.length >= 2, "PulseChainLibrary: INVALID_PATH");
        amounts = new uint256[](path.length);
        amounts[amounts.length - 1] = amountOut;
        for (uint256 i = path.length - 1; i > 0; i--) {
            (uint256 reserveIn, uint256 reserveOut) = getReserves(
                factory,
                path[i - 1],
                path[i]
            );
            amounts[i - 1] = getAmountIn(
                amounts[i],
                reserveIn,
                reserveOut,
                getSwapFee(factory, path[i - 1], path[i])
            );
        }
    }
}

// File: contracts\interfaces\IERC20.sol

interface IERC20 {
    event Approval(
        address indexed owner,
        address indexed spender,
        uint256 value
    );
    event Transfer(address indexed from, address indexed to, uint256 value);

    function name() external view returns (string memory);

    function symbol() external view returns (string memory);

    function decimals() external view returns (uint8);

    function totalSupply() external view returns (uint256);

    function balanceOf(address owner) external view returns (uint256);

    function allowance(address owner, address spender)
        external
        view
        returns (uint256);

    function approve(address spender, uint256 value) external returns (bool);

    function transfer(address to, uint256 value) external returns (bool);

    function transferFrom(
        address from,
        address to,
        uint256 value
    ) external returns (bool);
}

// File: contracts\interfaces\IWPULSE.sol

interface IWPULSE {
    function deposit() external payable;

    function transfer(address to, uint256 value) external returns (bool);

    function withdraw(uint256) external;
}

// File: contracts\PulseChainRouter.sol

contract PulseChainRouter is IPulseChainRouter02 {
    //using SafeMath for uint256;

    address public immutable override factory;
    address public immutable override WPULSE;
    //events  
    event RouterSwap(
        address indexed sender,
        uint amount0In,
        uint amount1In,
        uint amount0Out,
        uint amount1Out,
        address indexed to,
        address indexed pair
    );
    
    event addLiquid(
         address indexed sender,
         uint amount0,
         uint amount1,
         uint lpAmountMinted,
         address indexed to,
         address indexed pair);
    event removeLiquid(
         address indexed sender,
         uint amount0,
         uint amount1,
         uint lpAmountBurned,
         address indexed to,
         address indexed pair
         );
    modifier ensure(uint256 deadline) {
        require(deadline >= block.timestamp, "PulseChainRouter: EXPIRED");
        _;
    }

    constructor(address _factory, address _WPULSE) {
        factory = _factory;
        WPULSE = _WPULSE;
    }

    receive() external payable {
        assert(msg.sender == WPULSE); // only accept PULSE via fallback from the WPULSE contract
    }

    // **** ADD LIQUIDITY ****
    function _addLiquidity(
        address tokenA,
        address tokenB,
        uint256 amountADesired,
        uint256 amountBDesired,
        uint256 amountAMin,
        uint256 amountBMin
    ) internal virtual returns (uint256 amountA, uint256 amountB) {
        // create the pair if it doesn't exist yet
        if (IPulseChainFactory(factory).getPair(tokenA, tokenB) == address(0)) {
            IPulseChainFactory(factory).createPair(tokenA, tokenB);
        }
        (uint256 reserveA, uint256 reserveB) = PulseChainLibrary.getReserves(
            factory,
            tokenA,
            tokenB
        );
        if (reserveA == 0 && reserveB == 0) {
            (amountA, amountB) = (amountADesired, amountBDesired);
        } else {
            uint256 amountBOptimal = PulseChainLibrary.quote(
                amountADesired,
                reserveA,
                reserveB
            );
            if (amountBOptimal <= amountBDesired) {
                require(
                    amountBOptimal >= amountBMin,
                    "PulseChainRouter: INSUFFICIENT_B_AMOUNT"
                );
                (amountA, amountB) = (amountADesired, amountBOptimal);
            } else {
                uint256 amountAOptimal = PulseChainLibrary.quote(
                    amountBDesired,
                    reserveB,
                    reserveA
                );
                assert(amountAOptimal <= amountADesired);
                require(
                    amountAOptimal >= amountAMin,
                    "PulseChainRouter: INSUFFICIENT_A_AMOUNT"
                );
                (amountA, amountB) = (amountAOptimal, amountBDesired);
            }
        }
    }

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint256 amountADesired,
        uint256 amountBDesired,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    )
        external
        virtual
        override
        ensure(deadline)
        returns (
            uint256 amountA,
            uint256 amountB,
            uint256 liquidity
        )
    {
        (amountA, amountB) = _addLiquidity(
            tokenA,
            tokenB,
            amountADesired,
            amountBDesired,
            amountAMin,
            amountBMin
        );
        address pair = PulseChainLibrary.pairFor(factory, tokenA, tokenB);
        uint256 Bal_tokenA = IERC20(tokenA).balanceOf(pair);
        uint256 Bal_tokenB = IERC20(tokenB).balanceOf(pair);
        TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA);
        TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB);
        uint256 Bal_tokA = IERC20(tokenA).balanceOf(pair);
        uint256 Bal_tokB = IERC20(tokenB).balanceOf(pair);
        require(
            Bal_tokA - Bal_tokenA == amountA &&
                Bal_tokB - Bal_tokenB == amountB,
            "Amount Verification Failed"
        );
        uint amount0;
        uint amount1;
        (liquidity,amount0,amount1) = IPulseChainPair(pair).mint(to);
        //emit addLiquid(msg.sender,amountA,amountB,tokenA,tokenB,liquidity,to,pair);
        emit addLiquid(msg.sender,amount0,amount1,liquidity,to,pair);
    }

    function addLiquidityPULSE(
        address token,
        uint256 amountTokenDesired,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline
    )
        external
        payable
        virtual
        override
        ensure(deadline)
        returns (
            uint256 amountToken,
            uint256 amountPULSE,
            uint256 liquidity
        )
    {
        (amountToken, amountPULSE) = _addLiquidity(
            token,
            WPULSE,
            amountTokenDesired,
            msg.value,
            amountTokenMin,
            amountPULSEMin
        );
        address pair = PulseChainLibrary.pairFor(factory, token, WPULSE);
        uint256 Bal_token = IERC20(token).balanceOf(pair);
        uint256 Bal_WPULSE = IERC20(WPULSE).balanceOf(pair);
        TransferHelper.safeTransferFrom(token, msg.sender, pair, amountToken);
        IWPULSE(WPULSE).deposit{value: amountPULSE}();
        assert(IWPULSE(WPULSE).transfer(pair, amountPULSE));
        uint256 Bal_tok = IERC20(token).balanceOf(pair);
        uint256 Bal_Wth = IERC20(WPULSE).balanceOf(pair);
        require(
            Bal_tok - Bal_token == amountToken &&
                Bal_Wth - Bal_WPULSE == amountPULSE,
            "Amount Verification Failed"
        );
        uint amount0;
        uint amount1;
        (liquidity,amount0,amount1)  = IPulseChainPair(pair).mint(to);
        // refund dust PULSE, if any
        if (msg.value > amountPULSE)
        {
           TransferHelper.safeTransferPULSE(msg.sender, msg.value - amountPULSE);
        }
        
        emit addLiquid(msg.sender,amount0,amount1,liquidity,to,pair);
    }

    // **** REMOVE LIQUIDITY ****
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    )
        public
        virtual
        override
        ensure(deadline)
        returns (uint256 amountA, uint256 amountB)
    {
        address pair = PulseChainLibrary.pairFor(factory, tokenA, tokenB);
        // uint256 Bal_token = IPulseChainPair(pair).balanceOf(pair);
        IPulseChainPair(pair).transferFrom(msg.sender, pair, liquidity); // send liquidity to pair
        // uint256 Bal_lptoken = IPulseChainPair(pair).balanceOf(pair);
        // require(Bal_lptoken - Bal_token == liquidity,"LP Amount Verification Failed");
        (uint256 amount0, uint256 amount1) = IPulseChainPair(pair).burn(to);
        (address token0, ) = PulseChainLibrary.sortTokens(tokenA, tokenB);
        (amountA, amountB) = tokenA == token0
            ? (amount0, amount1)
            : (amount1, amount0);
        require(
            amountA >= amountAMin,
            "PulseChainRouter: INSUFFICIENT_A_AMOUNT"
        );
        require(
            amountB >= amountBMin,
            "PulseChainRouter: INSUFFICIENT_B_AMOUNT"
        );
        uint liquid=liquidity;
        emit removeLiquid(msg.sender,amount0,amount1,liquid,to,pair);
    }

    function removeLiquidityPULSE(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline
    )
        public
        virtual
        override
        ensure(deadline)
        returns (uint256 amountToken, uint256 amountPULSE)
    {
        (amountToken, amountPULSE) = removeLiquidity(
            token,
            WPULSE,
            liquidity,
            amountTokenMin,
            amountPULSEMin,
            address(this),
            deadline
        );
        //address pair = PulseChainLibrary.pairFor(factory, token, WPULSE);
        uint256 Bal_token = IERC20(token).balanceOf(to);
        TransferHelper.safeTransfer(token, to, amountToken);
        uint256 Bal_lptoken = IERC20(token).balanceOf(to);
        require(
            Bal_lptoken - Bal_token == amountToken,
            "LP Amount Verification Failed"
        );
        IWPULSE(WPULSE).withdraw(amountPULSE);
        TransferHelper.safeTransferPULSE(to, amountPULSE);
        
    }

    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external virtual override returns (uint256 amountA, uint256 amountB) {
        address pair = PulseChainLibrary.pairFor(factory, tokenA, tokenB);
        uint256 value = approveMax ? type(uint256).max : liquidity;
        IPulseChainPair(pair).permit(
            msg.sender,
            address(this),
            value,
            deadline,
            v,
            r,
            s
        );
        (amountA, amountB) = removeLiquidity(
            tokenA,
            tokenB,
            liquidity,
            amountAMin,
            amountBMin,
            to,
            deadline
        );
    }

    function removeLiquidityPULSEWithPermit(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    )
        external
        virtual
        override
        returns (uint256 amountToken, uint256 amountPULSE)
    {
        address pair = PulseChainLibrary.pairFor(factory, token, WPULSE);
        uint256 value = approveMax ? type(uint256).max : liquidity;
        IPulseChainPair(pair).permit(
            msg.sender,
            address(this),
            value,
            deadline,
            v,
            r,
            s
        );
        (amountToken, amountPULSE) = removeLiquidityPULSE(
            token,
            liquidity,
            amountTokenMin,
            amountPULSEMin,
            to,
            deadline
        );
    }

    // **** REMOVE LIQUIDITY (supporting fee-on-transfer tokens) ****
    function removeLiquidityPULSESupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline
    ) public virtual override ensure(deadline) returns (uint256 amountPULSE) {
        (, amountPULSE) = removeLiquidity(
            token,
            WPULSE,
            liquidity,
            amountTokenMin,
            amountPULSEMin,
            address(this),
            deadline
        );
        TransferHelper.safeTransfer(
            token,
            to,
            IERC20(token).balanceOf(address(this))
        );
        IWPULSE(WPULSE).withdraw(amountPULSE);
        TransferHelper.safeTransferPULSE(to, amountPULSE);
    }

    function removeLiquidityPULSEWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountPULSEMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external virtual override returns (uint256 amountPULSE) {
        address pair = PulseChainLibrary.pairFor(factory, token, WPULSE);
        uint256 value = approveMax ? type(uint256).max : liquidity;
        IPulseChainPair(pair).permit(
            msg.sender,
            address(this),
            value,
            deadline,
            v,
            r,
            s
        );
        amountPULSE = removeLiquidityPULSESupportingFeeOnTransferTokens(
            token,
            liquidity,
            amountTokenMin,
            amountPULSEMin,
            to,
            deadline
        );
    }

    // **** SWAP ****
    // requires the initial amount to have already been sent to the first pair
    function _swap(
        uint256[] memory amounts,
        address[] memory path,
        address _to
    ) internal virtual {
        for (uint256 i; i < path.length - 1; i++) {
            (address input, address output) = (path[i], path[i + 1]);
            (address token0, ) = PulseChainLibrary.sortTokens(input, output);
            uint256 amountOut = amounts[i + 1];
            (uint256 amount0Out, uint256 amount1Out) = input == token0
                ? (uint256(0), amountOut)
                : (amountOut, uint256(0));
            address to = i < path.length - 2
                ? PulseChainLibrary.pairFor(factory, output, path[i + 2])
                : _to;
            (uint amount0In,uint amount1In)=(IPulseChainPair(PulseChainLibrary.pairFor(factory, input, output)).swap(amount0Out, amount1Out, to, new bytes(0)));
            emit RouterSwap(msg.sender,amount0In,amount1In, amount0Out, amount1Out, to,PulseChainLibrary.pairFor(factory, input, output));
        }
    }

    function swapExactTokensForTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    )
        external
        virtual
        override
        ensure(deadline)
        returns (uint256[] memory amounts)
    {
        amounts = PulseChainLibrary.getAmountsOut(factory, amountIn, path);
        require(
            amounts[amounts.length - 1] >= amountOutMin,
            "PulseChainRouter: INSUFFICIENT_OUTPUT_AMOUNT"
        );
        uint256 Bal_Token = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        TransferHelper.safeTransferFrom(
            path[0],
            msg.sender,
            PulseChainLibrary.pairFor(factory, path[0], path[1]),
            amounts[0]
        );
        uint256 Bal_Tok = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        require(
            Bal_Tok - Bal_Token == amounts[0],
            "Token Amount Verification Failed"
        );

        _swap(amounts, path, to);
    }

    function swapTokensForExactTokens(
        uint256 amountOut,
        uint256 amountInMax,
        address[] calldata path,
        address to,
        uint256 deadline
    )
        external
        virtual
        override
        ensure(deadline)
        returns (uint256[] memory amounts)
    {
        amounts = PulseChainLibrary.getAmountsIn(factory, amountOut, path);
        require(
            amounts[0] <= amountInMax,
            "PulseChainRouter: EXCESSIVE_INPUT_AMOUNT"
        );
        uint256 Bal_Token = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        TransferHelper.safeTransferFrom(
            path[0],
            msg.sender,
            PulseChainLibrary.pairFor(factory, path[0], path[1]),
            amounts[0]
        );
        uint256 Bal_Tok = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        require(
            Bal_Tok - Bal_Token == amounts[0],
            "Token Amount Verification Failed"
        );
        _swap(amounts, path, to);
    }

    function swapExactPULSEForTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    )
        external
        payable
        virtual
        override
        ensure(deadline)
        returns (uint256[] memory amounts)
    {
        require(path[0] == WPULSE, "PulseChainRouter: INVALID_PATH");
        amounts = PulseChainLibrary.getAmountsOut(factory, msg.value, path);
        require(
            amounts[amounts.length - 1] >= amountOutMin,
            "PulseChainRouter: INSUFFICIENT_OUTPUT_AMOUNT"
        );
        IWPULSE(WPULSE).deposit{value: amounts[0]}();
        assert(
            IWPULSE(WPULSE).transfer(
                PulseChainLibrary.pairFor(factory, path[0], path[1]),
                amounts[0]
            )
        );
        _swap(amounts, path, to);
    }

    function swapTokensForExactPULSE(
        uint256 amountOut,
        uint256 amountInMax,
        address[] calldata path,
        address to,
        uint256 deadline
    )
        external
        virtual
        override
        ensure(deadline)
        returns (uint256[] memory amounts)
    {
        require(
            path[path.length - 1] == WPULSE,
            "PulseChainRouter: INVALID_PATH"
        );
        amounts = PulseChainLibrary.getAmountsIn(factory, amountOut, path);
        require(
            amounts[0] <= amountInMax,
            "PulseChainRouter: EXCESSIVE_INPUT_AMOUNT"
        );
        uint256 Bal_Token = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        TransferHelper.safeTransferFrom(
            path[0],
            msg.sender,
            PulseChainLibrary.pairFor(factory, path[0], path[1]),
            amounts[0]
        );
        uint256 Bal_Tok = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        require(
            Bal_Tok - Bal_Token == amounts[0],
            "Token Amount Verification Failed"
        );
        _swap(amounts, path, address(this));
        IWPULSE(WPULSE).withdraw(amounts[amounts.length - 1]);
        TransferHelper.safeTransferPULSE(to, amounts[amounts.length - 1]);
    }

    function swapExactTokensForPULSE(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    )
        external
        virtual
        override
        ensure(deadline)
        returns (uint256[] memory amounts)
    {
        require(
            path[path.length - 1] == WPULSE,
            "PulseChainRouter: INVALID_PATH"
        );
        amounts = PulseChainLibrary.getAmountsOut(factory, amountIn, path);
        require(
            amounts[amounts.length - 1] >= amountOutMin,
            "PulseChainRouter: INSUFFICIENT_OUTPUT_AMOUNT"
        );

        uint256 Bal_Token = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );

        TransferHelper.safeTransferFrom(
            path[0],
            msg.sender,
            PulseChainLibrary.pairFor(factory, path[0], path[1]),
            amounts[0]
        );
        uint256 Bal_Tok = IERC20(path[0]).balanceOf(
            PulseChainLibrary.pairFor(factory, path[0], path[1])
        );
        require(
            Bal_Tok - Bal_Token == amounts[0],
            "Token Amount Verification Failed"
        );
        _swap(amounts, path, address(this));
        IWPULSE(WPULSE).withdraw(amounts[amounts.length - 1]);
        TransferHelper.safeTransferPULSE(to, amounts[amounts.length - 1]);
    }

    function swapPULSEForExactTokens(
        uint256 amountOut,
        address[] calldata path,
        address to,
        uint256 deadline
    )
        external
        payable
        virtual
        override
        ensure(deadline)
        returns (uint256[] memory amounts)
    {
        require(path[0] == WPULSE, "PulseChainRouter: INVALID_PATH");
        amounts = PulseChainLibrary.getAmountsIn(factory, amountOut, path);
        require(
            amounts[0] <= msg.value,
            "PulseChainRouter: EXCESSIVE_INPUT_AMOUNT"
        );
        IWPULSE(WPULSE).deposit{value: amounts[0]}();
        assert(
            IWPULSE(WPULSE).transfer(
                PulseChainLibrary.pairFor(factory, path[0], path[1]),
                amounts[0]
            )
        );
        _swap(amounts, path, to);
        // refund dust PULSE, if any
        if (msg.value > amounts[0])
            TransferHelper.safeTransferPULSE(msg.sender, msg.value - amounts[0]);
    }

    // **** SWAP (supporting fee-on-transfer tokens) ****
    // requires the initial amount to have already been sent to the first pair
    function _swapSupportingFeeOnTransferTokens(
        address[] memory path,
        address _to
    ) internal virtual {
        for (uint256 i; i < path.length - 1; i++) {
            (address input, address output) = (path[i], path[i + 1]);
            (address token0, ) = PulseChainLibrary.sortTokens(input, output);
            IPulseChainPair pair = IPulseChainPair(
                PulseChainLibrary.pairFor(factory, input, output)
            );
            uint256 amountInput;
            uint256 amountOutput;
            {
                // scope to avoid stack too deep errors
                (uint256 reserve0, uint256 reserve1, ) = pair.getReserves();
                (uint256 reserveInput, uint256 reserveOutput) = input == token0
                    ? (reserve0, reserve1)
                    : (reserve1, reserve0);
                amountInput =
                    IERC20(input).balanceOf(address(pair)) -
                    (reserveInput);
                amountOutput = PulseChainLibrary.getAmountOut(
                    amountInput,
                    reserveInput,
                    reserveOutput,
                    pair.swapFee()
                );
            }
            (uint256 amount0Out, uint256 amount1Out) = input == token0
                ? (uint256(0), amountOutput)
                : (amountOutput, uint256(0));
            address to = i < path.length - 2
                ? PulseChainLibrary.pairFor(factory, output, path[i + 2])
                : _to;
            (uint amount0In,uint amount1In)=pair.swap(amount0Out, amount1Out, to, new bytes(0));
            emit RouterSwap(msg.sender,amount0In,amount1In, amount0Out, amount1Out,to,PulseChainLibrary.pairFor(factory, input, output));
        }
    }

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external virtual override ensure(deadline) {
        TransferHelper.safeTransferFrom(
            path[0],
            msg.sender,
            PulseChainLibrary.pairFor(factory, path[0], path[1]),
            amountIn
        );
        uint256 balanceBefore = IERC20(path[path.length - 1]).balanceOf(to);
        _swapSupportingFeeOnTransferTokens(path, to);
        require(
            IERC20(path[path.length - 1]).balanceOf(to) - (balanceBefore) >=
                amountOutMin,
            "PulseChainRouter: INSUFFICIENT_OUTPUT_AMOUNT"
        );
    }

    function swapExactPULSEForTokensSupportingFeeOnTransferTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable virtual override ensure(deadline) {
        require(path[0] == WPULSE, "PulseChainRouter: INVALID_PATH");
        uint256 amountIn = msg.value;
        IWPULSE(WPULSE).deposit{value: amountIn}();
        assert(
            IWPULSE(WPULSE).transfer(
                PulseChainLibrary.pairFor(factory, path[0], path[1]),
                amountIn
            )
        );
        uint256 balanceBefore = IERC20(path[path.length - 1]).balanceOf(to);
        _swapSupportingFeeOnTransferTokens(path, to);
        require(
            IERC20(path[path.length - 1]).balanceOf(to) - (balanceBefore) >=
                amountOutMin,
            "PulseChainRouter: INSUFFICIENT_OUTPUT_AMOUNT"
        );
    }

    function swapExactTokensForPULSESupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external virtual override ensure(deadline) {
        require(
            path[path.length - 1] == WPULSE,
            "PulseChainRouter: INVALID_PATH"
        );
        TransferHelper.safeTransferFrom(
            path[0],
            msg.sender,
            PulseChainLibrary.pairFor(factory, path[0], path[1]),
            amountIn
        );
        _swapSupportingFeeOnTransferTokens(path, address(this));
        uint256 amountOut = IERC20(WPULSE).balanceOf(address(this));
        require(
            amountOut >= amountOutMin,
            "PulseChainRouter: INSUFFICIENT_OUTPUT_AMOUNT"
        );
        IWPULSE(WPULSE).withdraw(amountOut);
        TransferHelper.safeTransferPULSE(to, amountOut);
    }

    // **** LIBRARY FUNCTIONS ****
    function quote(
        uint256 amountA,
        uint256 reserveA,
        uint256 reserveB
    ) public pure virtual override returns (uint256 amountB) {
        return PulseChainLibrary.quote(amountA, reserveA, reserveB);
    }

    function getAmountOut(
        uint256 amountIn,
        uint256 reserveIn,
        uint256 reserveOut,
        uint256 swapFee
    ) public pure virtual override returns (uint256 amountOut) {
        return
            PulseChainLibrary.getAmountOut(
                amountIn,
                reserveIn,
                reserveOut,
                swapFee
            );
    }

    function getAmountIn(
        uint256 amountOut,
        uint256 reserveIn,
        uint256 reserveOut,
        uint256 swapFee
    ) public pure virtual override returns (uint256 amountIn) {
        return
            PulseChainLibrary.getAmountIn(
                amountOut,
                reserveIn,
                reserveOut,
                swapFee
            );
    }

    function getAmountsOut(uint256 amountIn, address[] memory path)
        public
        view
        virtual
        override
        returns (uint256[] memory amounts)
    {
        return PulseChainLibrary.getAmountsOut(factory, amountIn, path);
    }

    function getAmountsIn(uint256 amountOut, address[] memory path)
        public
        view
        virtual
        override
        returns (uint256[] memory amounts)
    {
        return PulseChainLibrary.getAmountsIn(factory, amountOut, path);
    }
}