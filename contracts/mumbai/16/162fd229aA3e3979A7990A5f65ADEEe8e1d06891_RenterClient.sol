/// SPDX-License-Identifier: MIT
pragma solidity ^0.8.2;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}



/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 *
 * [WARNING]
 * ====
 *  Trying to delete such a structure from storage will likely result in data corruption, rendering the structure unusable.
 *  See https://github.com/ethereum/solidity/pull/11843[ethereum/solidity#11843] for more info.
 *
 *  In order to clean an EnumerableSet, you can either remove all elements one by one or create a fresh instance using an array of EnumerableSet.
 * ====
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastValue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastValue;
                // Update the index for the moved value
                set._indexes[lastValue] = valueIndex;
                // Replace lastValue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }
}

library Array256Lib {
    function removeAll(uint256[] memory array, uint256[] memory valuesToRemove) internal pure returns (uint256[] memory) {
        uint256[] memory newArray = new uint256[](array.length - valuesToRemove.length);
        uint idx = 0;
        for (uint i = 0; i < array.length; i++) {
            if (!contains(valuesToRemove, array[i])) {
                newArray[idx++] = array[i];
            }
        }
        require(newArray.length == array.length - valuesToRemove.length, "Failed to remove");
        return newArray;
    }

    function contains(uint256[] memory array, uint256 value) internal pure returns (bool) {
        for (uint i = 0; i < array.length; i++) {
            if (array[i] == value) {
                return true;
            }
        }
        return false;
    }


    function remove(uint256[] memory array, uint256 value) internal pure returns (uint256[] memory){
        uint256[] memory newArray = new uint256[](array.length - 1);
        uint idx = 0;
        for (uint i = 0; i < array.length; i++) {
            if (array[i] != value) {
                newArray[idx++] = array[i];
            }
        }
        require(newArray.length == array.length - 1, "Failed to remove");
        return newArray;
    }

    function add(uint256[] memory array, uint256 value) internal pure returns (uint256[] memory){
        uint256[] memory newArray = new uint256[](array.length + 1);
        for (uint i = 0; i < array.length; i++) {
            newArray[i] = array[i];
        }
        newArray[array.length] = value;
        return newArray;
    }

    function addAll(uint256[] memory array, uint256[] memory valuesToAdd) internal pure returns (uint256[] memory){
        uint256[] memory newArray = new uint256[](array.length + valuesToAdd.length);
        for (uint i = 0; i < array.length; i++) {
            newArray[i] = array[i];
        }
        for (uint i = array.length; i < array.length + valuesToAdd.length; i++) {
            newArray[i] = valuesToAdd[i - array.length];
        }
        return newArray;
    }
}


interface IRentingContract {

    enum Coin {
        XOIL,
        RBLS,
        WETH,
        USDC
    }

    enum RentingType {
        FIXED_PRICE,
        SPLIT
    }

    enum TokenRentingStatus {
        AVAILABLE,
        LISTED_BATTLE_SET,
        LISTED_COLLECTION,
        RENTED
    }

    struct BattleSet {
        uint256 landId;
        uint256[] botsIds;
    }

    struct RentingInfo {
        BattleSet battleSet;
        RentingType rentingType; // probaby change to uint
        Coin chargeCoin;// probaby change to uint
        uint256 price;
        address owner;
        address renter;
        uint256 rentingTs;
        uint256 renewTs;
        uint256 rentingEndTs;
        uint256 cancelTs;
        uint256 collectionId;
        bool perpetual;
        address[] whitelist;
    }

    struct Collection {
        uint256 id;
        address owner;
        uint256[] landIds;
        uint256[] botsIds;
        uint256[] rentedLandIds;
        uint256[] rentedBotsIds;
        uint256[] landsToRemove;
        uint256[] botsToRemove;
        address[] whitelist;
        RentingType rentingType;
        Coin chargeCoin;// probaby change to uint
        uint256 price;
        uint share;
        bool perpetual;
        uint256 disbandTs;
    }

    struct ListingInfo {
        BattleSet battleSet;
        RentingType rentingType;
        Coin chargeCoin;
        uint256 listingTs;
        address owner;
        uint256 price;
        bool perpetual;
        address[] whitelist;
    }

    struct PaymentData {
        RentingType rentingType;
        Coin coin;
        uint256 price;
    }
}


interface IERC20 {
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);
}


interface IRentingContractStorage is IRentingContract {

    function renewRenting(uint256 id, uint256 renewTs, uint256 rentingEndTs) external;

    function getRentingInfo(uint256 landId) external view returns (RentingInfo memory);

    function getCollection(uint256 id) external view returns (Collection memory);

    function createRenting(BattleSet memory bs, RentingType rt, Coin coin, uint256 price, address owner, address renter,
        uint256 rentingEnd, uint256 collectionId, bool perpetual, address[] memory whitelist) external;

    function deleteListingInfo(uint256 landId) external;

    function getListingInfo(uint256 landId) external view returns (ListingInfo memory);

    function updateCollectionRentedAssets(uint256 id, uint256[] memory availableLands, uint256[] memory availableBotsIds,
        uint256[] memory rentedLandIds, uint256[] memory rentedBotsIds) external;

    function rentedLandsByOwner(address owner) external view returns (uint);

    function rentedOwnersLandsByIndex(address owner, uint256 idx) external view returns (uint256);

    function completeRenting(uint256 landId) external;
}


interface IERC721RentingContract {

    function safeTransferFromForRent(address from, address to, uint256 tokenId, bytes memory _data) external;

    function ownerOf(uint256 tokenId) external view returns (address owner);

    function isTokenRented(uint256 tokenId) external view returns (bool);

    function safeTransferFrom(address from, address to, uint256 tokenId) external;
}

abstract contract TokensManager {
    IERC721RentingContract public landsContract;
    IERC721RentingContract public botsContract;

    constructor(address landsContractAddress, address botsContractAddress) {
        landsContract = IERC721RentingContract(landsContractAddress);
        botsContract = IERC721RentingContract(botsContractAddress);
    }

    function userOwnsLands(uint256[] memory landIds, address owner) internal view returns (bool) {

        for (uint i = 0; i < landIds.length; i++) {
            if (landsContract.ownerOf(landIds[i]) != owner || landsContract.isTokenRented(landIds[i])) {
                return false;
            }
        }
        return true;
    }

    function userOwnsBots(uint256[] memory botIds, address owner) internal view returns (bool) {
        for (uint i = 0; i < botIds.length; i++) {
            if (botsContract.ownerOf(botIds[i]) != owner || botsContract.isTokenRented(botIds[i])) {
                return false;
            }
        }
        return true;
    }

    function safeTransferFromSetForRent(address from, address to, uint256 landId, uint256[] memory botIds) internal {
        landsContract.safeTransferFromForRent(from, to, landId, "");
        for (uint i = 0; i < botIds.length; i++) {
            botsContract.safeTransferFromForRent(from, to, botIds[i], "");
        }
    }
}

contract RenterClient is Context, IRentingContract, TokensManager {

    using Array256Lib for uint256[];
    using EnumerableSet for EnumerableSet.UintSet;

    //    uint private constant default_renting_duration = 7 days;
//    uint private constant DEFAULT_RENTING_DURATION = 5 hours;
//    uint private constant RENTING_RENEWAL_PERIOD_GAP = 4 hours;

    uint private constant DEFAULT_RENTING_DURATION = 5 minutes;
    uint private constant RENTING_RENEWAL_PERIOD_GAP = 4 minutes;

    uint256[] private rentedLands;
    mapping(uint256 => uint256) private rentedLandsIndex;

    mapping(address => EnumerableSet.UintSet) private _rentedBattleSetsOwnersMapping;

    mapping(Coin => address) paymentContracts;
    IRentingContractStorage internal storageContract;

    event RentBattleSetStart(uint256 indexed landId, uint256[] botIds, address renter, address owner);
    event RentCollectionStart(uint256 indexed landId, uint256[] botIds,  uint256 collectionId, address renter, address owner);
    event RentRenewed(uint256 indexed landId);
    event RentEnd(uint256 indexed landId, uint256 collectionId, address renter, address owner);

    constructor(address storageContractAddress, address landsContractAddress, address botsContractAddress, address xoilAddress, address rblsAddress)
    TokensManager(landsContractAddress, botsContractAddress) {
        paymentContracts[Coin.XOIL] = xoilAddress;
        paymentContracts[Coin.RBLS] = rblsAddress;
        storageContract = IRentingContractStorage(storageContractAddress);
    }


    function addressWhitelisted(address[] memory array, address value) internal pure returns (bool) {
        for (uint i = 0; i < array.length; i++) {
            if (array[i] == value) {
                return true;
            }
        }
        return false;
    }

    function rentBattleSet(uint256 landId) external returns (bool) {
        ListingInfo memory li = storageContract.getListingInfo(landId);
        require(li.listingTs != 0, "Listing not found");
        if (li.whitelist.length > 0) {
            require(addressWhitelisted(li.whitelist, _msgSender()), "Address not whitelisted");
        }

        IERC20 paymentContract = IERC20(paymentContracts[li.chargeCoin]);
        if (!paymentContract.transferFrom(_msgSender(), li.owner, li.price)) {
            return false;
        }

        storageContract.deleteListingInfo(landId);

        super.safeTransferFromSetForRent(li.owner, _msgSender(), li.battleSet.landId, li.battleSet.botsIds);

        storageContract.createRenting(li.battleSet, li.rentingType, li.chargeCoin, li.price, li.owner, _msgSender(), block.timestamp + DEFAULT_RENTING_DURATION,
            0, li.perpetual, li.whitelist);

        addTokenFromRentedTokensList(li.battleSet.landId);
        _rentedBattleSetsOwnersMapping[li.owner].add(landId);

        emit RentBattleSetStart(landId, li.battleSet.botsIds, _msgSender(), li.owner);
        return true;
    }

    function rentedLandsByOwner(address owner) external view returns (uint256) {
        return _rentedBattleSetsOwnersMapping[owner].length();
    }

    function rentedOwnersLandsByIndex(address owner, uint256 idx) external view returns (uint256) {
        return _rentedBattleSetsOwnersMapping[owner].at(idx);
    }



    function rentFromCollection(uint256 id, uint256 landId, uint256[] memory botsIds) external returns (bool) {
        Collection memory collection = storageContract.getCollection(id);
        require(collection.owner != address(0x0), "Collection not found");
        require(collection.disbandTs == 0, "Collection disbanded");
        if (collection.whitelist.length > 0) {
            require(addressWhitelisted(collection.whitelist, _msgSender()), "Address not whitelisted");
        }

        IERC20 paymentContract = IERC20(paymentContracts[collection.chargeCoin]);
        if (!paymentContract.transferFrom(_msgSender(), collection.owner, collection.price)) {
            return false;
        }

        super.safeTransferFromSetForRent(collection.owner, _msgSender(), landId, botsIds);

        storageContract.updateCollectionRentedAssets(id, Array256Lib.remove(collection.landIds, landId),
            Array256Lib.removeAll(collection.botsIds, botsIds), Array256Lib.add(collection.rentedLandIds, landId),
            Array256Lib.addAll(collection.rentedBotsIds, botsIds));

        BattleSet memory bs = BattleSet({landId : landId, botsIds : botsIds});
        storageContract.createRenting(bs, collection.rentingType, collection.chargeCoin, collection.price, collection.owner, _msgSender(),
            block.timestamp + DEFAULT_RENTING_DURATION, collection.id, collection.perpetual, new address[](0));

        addTokenFromRentedTokensList(bs.landId);
        _rentedBattleSetsOwnersMapping[collection.owner].add(landId);

        emit RentCollectionStart(landId, botsIds, id, _msgSender(), collection.owner);
        return true;
    }

    function renewRental(uint256 landId) external returns (bool){
        RentingInfo memory ri = storageContract.getRentingInfo(landId);
        require(ri.rentingTs != 0, "Land is not rented");
        require(ri.perpetual, "The listing is not perpetual");
        require(ri.cancelTs == 0, "The listing is cancelled");
        require(ri.renewTs < ri.rentingEndTs - RENTING_RENEWAL_PERIOD_GAP, "Already renewed for next period");
        require(ri.renter == _msgSender(), "Caller is not renter");
        require(block.timestamp < ri.rentingEndTs && block.timestamp > ri.rentingEndTs - RENTING_RENEWAL_PERIOD_GAP, "Renew is not available yet");
        if (ri.collectionId != 0) {
            Collection memory collection = storageContract.getCollection(ri.collectionId);
            require(collection.disbandTs == 0, "Collection disbanded");
            require(addressWhitelistedInCollection(collection, _msgSender()), "Player not whitelisted to renew listing");
            require(!ifCollectionAssetNeedsToBeRemoved(ri.collectionId, ri.battleSet.landId, ri.battleSet.botsIds), "Some asset removed from collection");
        }

        IERC20 paymentContract = IERC20(paymentContracts[ri.chargeCoin]);
        if (!paymentContract.transferFrom(_msgSender(), ri.owner, ri.price)) {
            return false;
        }

        storageContract.renewRenting(landId, block.timestamp, ri.rentingEndTs + DEFAULT_RENTING_DURATION);

        emit RentRenewed(landId);
        return true;
    }


    function ifCollectionAssetNeedsToBeRemoved(uint256 collectionId, uint256 landId, uint256[] memory botIds) private view returns (bool) {
        Collection memory collection = storageContract.getCollection(collectionId);
        if (Array256Lib.contains(collection.landsToRemove, landId)) {
            return true;
        }
        for (uint i = 0; i < botIds.length; i++) {
            if (Array256Lib.contains(collection.botsToRemove, botIds[i])) {
                return true;
            }
        }
        return false;
    }


    function addressWhitelistedInCollection(Collection memory collection, address player) private pure returns (bool){
        for (uint i = 0; i < collection.whitelist.length; i++) {
            if (collection.whitelist[i] == player) {
                return true;
            }
        }
        return false;
    }

    function getTotalRentings() external view returns (uint256) {
        return rentedLands.length;
    }

    function rentedLandByIdx(uint idx) external view returns (uint256) {
        return rentedLands[idx];
    }

    function completeRentings(uint256[] memory landIds) external {
        for (uint i = 0; i < landIds.length; i++) {
            if (storageContract.getRentingInfo(landIds[i]).rentingEndTs <= block.timestamp) {
                completeRenting(landIds[i]);
            }
        }
    }

    function completeRenting(uint256 landId) private {
        RentingInfo memory ri = storageContract.getRentingInfo(landId);
        if (ri.rentingTs == 0 || ri.rentingEndTs > block.timestamp) {
            return;
        }

        landsContract.safeTransferFrom(ri.renter, ri.owner, ri.battleSet.landId);
        for (uint i = 0; i < 3; i++) {
            botsContract.safeTransferFrom(ri.renter, ri.owner, ri.battleSet.botsIds[i]);
        }

        storageContract.completeRenting(landId);
        _removeTokenFromRentedTokensList(landId);
        _rentedBattleSetsOwnersMapping[ri.owner].remove(ri.battleSet.landId);

        emit RentEnd(landId, ri.collectionId, ri.renter, ri.owner);
    }

    function addTokenFromRentedTokensList(uint256 tokenId) private {
        rentedLandsIndex[tokenId] = rentedLands.length;
        rentedLands.push(tokenId);
    }

    function _removeTokenFromRentedTokensList(uint256 tokenId) private {
        uint256 lastTokenIndex = rentedLands.length - 1;
        uint256 tokenIndex = rentedLandsIndex[tokenId];

        uint256 lastTokenId = rentedLands[lastTokenIndex];

        rentedLands[tokenIndex] = lastTokenId;
        rentedLandsIndex[lastTokenId] = tokenIndex;

        delete rentedLandsIndex[tokenId];
        rentedLands.pop();
    }
}