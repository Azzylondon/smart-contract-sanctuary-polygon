// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal onlyInitializing {
        __Context_init_unchained();
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal onlyInitializing {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (proxy/utils/Initializable.sol)

pragma solidity ^0.8.0;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To initialize the implementation contract, you can either invoke the
 * initializer manually, or you can include a constructor to automatically mark it as initialized when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() initializer {}
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, because in other contexts the
        // contract may have been reentered.
        require(_initializing ? _isConstructor() : !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} modifier, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    function _isConstructor() private view returns (bool) {
        return !AddressUpgradeable.isContract(address(this));
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract PausableUpgradeable is Initializable, ContextUpgradeable {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    function __Pausable_init() internal onlyInitializing {
        __Context_init_unchained();
        __Pausable_init_unchained();
    }

    function __Pausable_init_unchained() internal onlyInitializing {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165Upgradeable.sol";

/**
 * @dev _Available since v3.1._
 */
interface IERC1155ReceiverUpgradeable is IERC165Upgradeable {
    /**
        @dev Handles the receipt of a single ERC1155 token type. This function is
        called at the end of a `safeTransferFrom` after the balance has been updated.
        To accept the transfer, this must return
        `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
        (i.e. 0xf23a6e61, or its own function selector).
        @param operator The address which initiated the transfer (i.e. msg.sender)
        @param from The address which previously owned the token
        @param id The ID of the token being transferred
        @param value The amount of tokens being transferred
        @param data Additional data with no specified format
        @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
    */
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);

    /**
        @dev Handles the receipt of a multiple ERC1155 token types. This function
        is called at the end of a `safeBatchTransferFrom` after the balances have
        been updated. To accept the transfer(s), this must return
        `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
        (i.e. 0xbc197c81, or its own function selector).
        @param operator The address which initiated the batch transfer (i.e. msg.sender)
        @param from The address which previously owned the token
        @param ids An array containing ids of each token being transferred (order and length must match values array)
        @param values An array containing amounts of each token being transferred (order and length must match ids array)
        @param data Additional data with no specified format
        @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
    */
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165Upgradeable.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155Upgradeable is IERC165Upgradeable {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/utils/ERC1155Holder.sol)

pragma solidity ^0.8.0;

import "./ERC1155ReceiverUpgradeable.sol";
import "../../../proxy/utils/Initializable.sol";

/**
 * @dev _Available since v3.1._
 */
contract ERC1155HolderUpgradeable is Initializable, ERC1155ReceiverUpgradeable {
    function __ERC1155Holder_init() internal onlyInitializing {
        __ERC165_init_unchained();
        __ERC1155Receiver_init_unchained();
        __ERC1155Holder_init_unchained();
    }

    function __ERC1155Holder_init_unchained() internal onlyInitializing {
    }
    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] memory,
        uint256[] memory,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC1155/utils/ERC1155Receiver.sol)

pragma solidity ^0.8.0;

import "../IERC1155ReceiverUpgradeable.sol";
import "../../../utils/introspection/ERC165Upgradeable.sol";
import "../../../proxy/utils/Initializable.sol";

/**
 * @dev _Available since v3.1._
 */
abstract contract ERC1155ReceiverUpgradeable is Initializable, ERC165Upgradeable, IERC1155ReceiverUpgradeable {
    function __ERC1155Receiver_init() internal onlyInitializing {
        __ERC165_init_unchained();
        __ERC1155Receiver_init_unchained();
    }

    function __ERC1155Receiver_init_unchained() internal onlyInitializing {
    }
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165Upgradeable, IERC165Upgradeable) returns (bool) {
        return interfaceId == type(IERC1155ReceiverUpgradeable).interfaceId || super.supportsInterface(interfaceId);
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721Receiver.sol)

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721ReceiverUpgradeable {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165Upgradeable.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721Upgradeable is IERC165Upgradeable {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Enumerable.sol)

pragma solidity ^0.8.0;

import "../IERC721Upgradeable.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721EnumerableUpgradeable is IERC721Upgradeable {
    /**
     * @dev Returns the total amount of tokens stored by the contract.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.
     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256 tokenId);

    /**
     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.
     * Use along with {totalSupply} to enumerate all tokens.
     */
    function tokenByIndex(uint256 index) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/utils/ERC721Holder.sol)

pragma solidity ^0.8.0;

import "../IERC721ReceiverUpgradeable.sol";
import "../../../proxy/utils/Initializable.sol";

/**
 * @dev Implementation of the {IERC721Receiver} interface.
 *
 * Accepts all token transfers.
 * Make sure the contract is able to use its token with {IERC721-safeTransferFrom}, {IERC721-approve} or {IERC721-setApprovalForAll}.
 */
contract ERC721HolderUpgradeable is Initializable, IERC721ReceiverUpgradeable {
    function __ERC721Holder_init() internal onlyInitializing {
        __ERC721Holder_init_unchained();
    }

    function __ERC721Holder_init_unchained() internal onlyInitializing {
    }
    /**
     * @dev See {IERC721Receiver-onERC721Received}.
     *
     * Always returns `IERC721Receiver.onERC721Received.selector`.
     */
    function onERC721Received(
        address,
        address,
        uint256,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC721Received.selector;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;
import "../proxy/utils/Initializable.sol";

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
        __Context_init_unchained();
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165Upgradeable.sol";
import "../../proxy/utils/Initializable.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165Upgradeable is Initializable, IERC165Upgradeable {
    function __ERC165_init() internal onlyInitializing {
        __ERC165_init_unchained();
    }

    function __ERC165_init_unchained() internal onlyInitializing {
    }
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165Upgradeable).interfaceId;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165Upgradeable {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSetUpgradeable {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./Utilities.sol";

contract Adminable is Utilities {

    mapping(address => bool) private admins;

    function addAdmin(address _address) external onlyOwner {
        require(_address.code.length > 0, "must be a contract");
        admins[_address] = true;
    }

    function addAdmins(address[] calldata _addresses) external onlyOwner {
        uint256 len = _addresses.length;
        for(uint256 i = 0; i < len; i++) {
            require(_addresses[i].code.length > 0, "must be a contract");
            admins[_addresses[i]] = true;
        }
    }

    function removeAdmin(address _address) external onlyOwner {
        admins[_address] = false;
    }

    function removeAdmins(address[] calldata _addresses) external onlyOwner {
        uint256 len = _addresses.length;
        for(uint256 i = 0; i < len; i++) {
            admins[_addresses[i]] = false;
        }
    }

    function setPause(bool _shouldPause) external onlyAdminOrOwner {
        if(_shouldPause) {
            _pause();
        } else {
            _unpause();
        }
    }

    function isAdmin(address _address) public view returns(bool) {
        return admins[_address];
    }

    modifier onlyAdmin() {
        require(admins[msg.sender], "Not admin");
        _;
    }

    modifier onlyAdminOrOwner() {
        require(admins[msg.sender] || isOwner(), "Not admin or owner");
        _;
    }
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./UtilitiesUpgradeable.sol";

// Do not add state to this contract.
//
contract AdminableUpgradeable is UtilitiesUpgradeable {

    mapping(address => bool) private admins;

    function __Adminable_init() internal initializer {
        UtilitiesUpgradeable.__Utilities__init();
    }

    function addAdmin(address _address) external onlyOwner {
        admins[_address] = true;
    }

    function addAdmins(address[] calldata _addresses) external onlyOwner {
        for(uint256 i = 0; i < _addresses.length; i++) {
            admins[_addresses[i]] = true;
        }
    }

    function removeAdmin(address _address) external onlyOwner {
        admins[_address] = false;
    }

    function removeAdmins(address[] calldata _addresses) external onlyOwner {
        for(uint256 i = 0; i < _addresses.length; i++) {
            admins[_addresses[i]] = false;
        }
    }

    function setPause(bool _shouldPause) external onlyAdminOrOwner {
        if(_shouldPause) {
            _pause();
        } else {
            _unpause();
        }
    }

    function isAdmin(address _address) public view returns(bool) {
        return admins[_address];
    }

    modifier onlyAdmin() {
        require(admins[msg.sender], "Not admin");
        _;
    }

    modifier onlyAdminOrOwner() {
        require(admins[msg.sender] || isOwner(), "Not admin or owner");
        _;
    }
    uint256[50] private __gap;
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/Pausable.sol";

contract Utilities is Ownable, Pausable {

    modifier nonZeroAddress(address _address) {
        require(address(0) != _address, "0 address");
        _;
    }

    modifier nonZeroLength(uint[] memory _array) {
        require(_array.length > 0, "Empty array");
        _;
    }

    modifier lengthsAreEqual(uint[] memory _array1, uint[] memory _array2) {
        require(_array1.length == _array2.length, "Unequal lengths");
        _;
    }

    modifier onlyEOA() {
        /* solhint-disable avoid-tx-origin */
        require(msg.sender == tx.origin, "No contracts");
        _;
    }

    function isOwner() internal view returns(bool) {
        return owner() == msg.sender;
    }
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/security/PausableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

contract UtilitiesUpgradeable is Initializable, OwnableUpgradeable, PausableUpgradeable {

    function __Utilities__init() internal initializer {
        OwnableUpgradeable.__Ownable_init();
        PausableUpgradeable.__Pausable_init();

        _pause();
    }

    modifier nonZeroAddress(address _address) {
        require(address(0) != _address, "0 address");
        _;
    }

    modifier nonZeroLength(uint[] memory _array) {
        require(_array.length > 0, "Empty array");
        _;
    }

    modifier lengthsAreEqual(uint[] memory _array1, uint[] memory _array2) {
        require(_array1.length == _array2.length, "Unequal lengths");
        _;
    }

    modifier onlyEOA() {
        /* solhint-disable avoid-tx-origin */
        require(msg.sender == tx.origin, "No contracts");
        _;
    }

    function isOwner() internal view returns(bool) {
        return owner() == msg.sender;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IRandomizerCL {
    // Returns a request ID for the random number. This should be kept and mapped to whatever the contract
    // is tracking randoms for.
    // Admin only.
    function getRandomNumber() external returns(bytes32);

    // Returns the random for the given request ID.
    // Will revert if the random is not ready.
    function randomForRequestID(bytes32 _requestID) external view returns(uint256);

    // Returns if the request ID has been fulfilled yet.
    function isRequestIDFulfilled(bytes32 _requestID) external view returns(bool);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IGraveyard {
    // sends the given wizard tokenId to this contract to be considered 'dead'. Can be revived though, so that's cool.
    function killWizard(uint256 _tokenId, address _owner) external;
    // attempts to bring the given dead wizard tokenId back to life and give to caller. This token must have been owned by the caller when it died.
    function reviveWizard(uint256 _tokenId) external;
    // Returns the rift tier for the given user based on how much GP is staked at the rift.
    function getFallenWizardsForAddr(address _address) external view returns(uint256[] memory);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../shared/IDragonStakable.sol";

interface IRift is IDragonStakable {

    // Returns the rift tier for the given user based on how much GP is staked at the rift.
    function getRiftTier(address _address) external view returns(uint256);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IDragonStakable {
    function stake(uint256 _tokenId, address _owner) external;
    function unstake(uint256 _tokenId) external;
}

// SPDX-License-Identifier: MIT LICENSE

pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC1155/IERC1155Upgradeable.sol";

interface IConsumables is IERC1155Upgradeable {
    function mint(uint256 typeId, uint256 qty, address recipient) external;
    function burn(uint256 typeId, uint256 qty, address burnFrom) external;
    function adminSafeTransferFrom(address _from, address _to, uint256 _id, uint256 _amount) external;
    function adminSafeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _amounts) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";

interface IGP is IERC20Upgradeable {
    function mint(address to, uint256 amount) external;
    function burn(address from, uint256 amount) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC1155/IERC1155Upgradeable.sol";

interface ISacrificialAlter is IERC1155Upgradeable {
    function mint(uint256 typeId, uint16 qty, address recipient) external;
    function burn(uint256 typeId, uint16 qty, address burnFrom) external;
    function adminSafeTransferFrom(address _from, address _to, uint256 _id, uint256 _amount) external;
    function adminSafeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _amounts) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC721/extensions/IERC721EnumerableUpgradeable.sol";

interface IWnDRoot {
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external;
    function getTokenTraits(uint256 _tokenId) external returns(WizardDragon memory);
    function ownerOf(uint256 _tokenId) external returns(address);
    function approve(address _to, uint256 _tokenId) external;
}

interface IWnD is IERC721EnumerableUpgradeable {
    function mint(address _to, uint256 _tokenId, WizardDragon calldata _traits) external;
    function burn(uint256 _tokenId) external;
    function isWizard(uint256 _tokenId) external view returns(bool);
    function getTokenTraits(uint256 _tokenId) external view returns(WizardDragon memory);
    function exists(uint256 _tokenId) external view returns(bool);
    function adminTransferFrom(address _from, address _to, uint256 _tokenId) external;
}

struct WizardDragon {
    bool isWizard;
    uint8 body;
    uint8 head;
    uint8 spell;
    uint8 eyes;
    uint8 neck;
    uint8 mouth;
    uint8 wand;
    uint8 tail;
    uint8 rankIndex;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface ITrainingGame {

    // Trains the given wizard. Must be staked at the training grounds.
    function train(uint256 _tokenId, bool equipWizard) external;

    // Reveals the reward for the given token id.
    function revealTrainingReward(uint256 _tokenId) external;

    // Indicates if the given wizard can play the game.
    function canWizardPlay(uint256 _tokenId) external view returns(bool);

    // Returns the timestamp the wizard can next play the game. May be in the past.
    function timeWizardCanPlayNext(uint256 _tokenId) external view returns(uint256);

    // Returns if a wizard is currently training.
    function isWizardTraining(uint256 _tokenId) external view returns(bool);

    // Resets the stats of the Wizard
    function resetWizard(uint256 _tokenId) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/utils/structs/EnumerableSetUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./TrainingGameContracts.sol";
import "./TrainingGameTimeKeeper.sol";
import "./TrainingGameArmor.sol";
import "./TrainingGameRewards.sol";

contract TrainingGame is Initializable, TrainingGameRewards, TrainingGameArmor {
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.UintSet;

    function initialize() external initializer {
        TrainingGameRewards.__TrainingGameRewards_init();
        TrainingGameArmor.__TrainingGameArmor_init();
    }

    function setTrainingSettings(
        uint256 _trainingFee,
        uint8 _baseChanceWizardBurned,
        uint8 _maxBurnReduction)
        external onlyAdminOrOwner
    {
        require(_baseChanceWizardBurned >= _maxBurnReduction, "Bad settings");

        trainingFee = _trainingFee;
        baseChanceWizardBurned = _baseChanceWizardBurned;
        maxBurnReduction = _maxBurnReduction;
    }

    function train(uint256 _tokenId, bool equipWizard) external override contractsAreSet whenNotPaused trainingRewardsSet onlyEOA {
        require(wnd.isWizard(_tokenId), "Not a wizard");
        require(canWizardPlay(_tokenId), "Wizard must cool down");
        require(!isWizardTraining(_tokenId), "Already training");

        address _owner = world.ownerOfTokenId(_tokenId);
        require(_owner == msg.sender, "Not owner of token");

        uint256 _trainingFee = _getTrainingFee(_tokenId);

        require(gp.balanceOf(msg.sender) >= _trainingFee, "Not enough GP");

        // Must actually be at the training grounds.
        require(world.locationOfToken(_tokenId) == Location.TRAINING_GROUNDS, "Not staked at TG");
        if(equipWizard) {
            require(consumables.balanceOf(_msgSender(), equipmentTokenId) > 0, "No armor to equip");
            equipArmor(_tokenId, _owner);
        }

        updateNextPlayTime(_tokenId, msg.sender);

        gp.burn(msg.sender, _trainingFee);

        if(tokenIdToInfo[_tokenId].gamesPlayed == 0
            && tokenIdToInfo[_tokenId].hp == 0)
        {
            tokenIdToInfo[_tokenId].hp = maxHPForToken(_tokenId);
        }

        bytes32 _requestId = randomizer.getRandomNumber();
        tokenIdToInfo[_tokenId].requestId = _requestId;

        emit TrainingStarted(_owner, _tokenId, _requestId);
    }

    function revealTrainingReward(uint256 _tokenId) external override contractsAreSet trainingRewardsSet {
        bytes32 _requestId = tokenIdToInfo[_tokenId].requestId;
        require(_requestId != 0, "Not training");
        require(randomizer.isRequestIDFulfilled(_requestId), "No random yet");

        address _owner = world.ownerOfTokenId(_tokenId);
        require(_owner == msg.sender || isAdmin(msg.sender), "Bad permissions");

        uint256 _randomness = randomizer.randomForRequestID(_requestId);

        _processReward(_tokenId, _owner, _randomness);
    }

    function _processReward(uint256 _tokenId, address _owner, uint256 _randomness) private {

        uint8 _wizardProficiency = trainingProficiency.proficiencyForWizard(_tokenId);

        uint8 _chanceWizardKilled = _chanceWizardDies(_wizardProficiency);

        bool _wasWizardKilled;

        uint256 _killResult = _randomness % 100;
        bool wouldDie = _killResult < _chanceWizardKilled;
        if(wouldDie && !hasArmor(_tokenId)) {
            // Get killed.
            graveyard.killWizard(_tokenId, _owner);
            _wasWizardKilled = true;
        } else {
            if(wouldDie && hasArmor(_tokenId)) {
                burnArmor(_tokenId);
            }
           _randomness = uint256(keccak256(abi.encode(_randomness, _randomness)));

           uint256 _damageRoll = _randomness % 100;

           uint256 _damageChance = _chanceWizardDamaged(_wizardProficiency);

           if(_damageRoll < _damageChance) {
                uint8 damageTaken = baseDamage;
                if(hasArmor(_tokenId)) {
                    damageTaken -= armorDamageReductionAmt;
                    burnArmor(_tokenId);
                }
                // Death
                if(tokenIdToInfo[_tokenId].hp <= damageTaken) {
                    tokenIdToInfo[_tokenId].hp = 0;
                    graveyard.killWizard(_tokenId, _owner);
                    _wasWizardKilled = true;
                } else {
                    tokenIdToInfo[_tokenId].hp -= damageTaken;
                }
           }

            if(!_wasWizardKilled) {
                // To the rewards!
                uint256 _newRandom = uint256(keccak256(abi.encode(_randomness, _tokenId)));

                _findAndSendRewards(_tokenId, _owner, _newRandom, _wizardProficiency);

                _incrementGamePlayed(_tokenId, _wizardProficiency);
            }

            if(hasArmor(_tokenId)) {
                unequipArmor(_tokenId, _owner);
            }
        }

        // Clear so they can play again
        delete tokenIdToInfo[_tokenId].requestId;
    }

    function _incrementGamePlayed(uint256 _tokenId, uint8 _proficiencyCur) private {
        tokenIdToInfo[_tokenId].gamesPlayed++;
        // Not at the highest proficiency level and are that the number of games needed for the next proficiency
        if(_proficiencyCur < proficiencyToGamesPlayed.length - 1 && tokenIdToInfo[_tokenId].gamesPlayed == proficiencyToGamesPlayed[_proficiencyCur + 1]) {
            trainingProficiency.increaseProficiencyForWizard(_tokenId);
            // Increase HP as well if needed.
            tokenIdToInfo[_tokenId].hp++;
        }
    }

    function _getTrainingFee(uint256 _tokenId) private view returns(uint256) {
        if(_tokenId <= 15000 && tokenIdToInfo[_tokenId].gamesPlayed == 0) {
            return 0;
        }
        uint8 _wizardProficiency = trainingProficiency.proficiencyForWizard(_tokenId);
        return trainingFee - (_wizardProficiency * 1000 ether);
    }

    function setProficiencyToGamesPlayed(uint8[] calldata _proficiencyToGamesPlayed) external onlyAdminOrOwner {
        proficiencyToGamesPlayed = _proficiencyToGamesPlayed;
    }

    function _findAndSendRewards(uint256 _tokenId, address _owner, uint256 _random, uint8 _wizardProficiency) private {
        uint256 _rewardId = pickReward(_random);
        _random >>= 32; // shuffle seed for new random
        uint256 _gpAmt = pickGPReward(_random);

        uint256 _newRandom = uint256(keccak256(abi.encode(_random, _random)));
        uint16 _amount = pickAmount(_rewardId, _newRandom, _wizardProficiency);

        consumables.mint(_rewardId, _amount, _owner);
        if(_gpAmt > 0) {
            gp.mint(_owner, _gpAmt);
        }

        emit RewardMinted(_owner, _tokenId, _rewardId, _amount, _gpAmt);
    }

    function _chanceWizardDies(uint8 _wizardProficiency) private view returns(uint8) {
        uint8 _chanceWizardBurned = baseChanceWizardBurned;
        // Lower the % chance to be burned by 1% per proficiency level up to a max amount
        if(_wizardProficiency > maxBurnReduction) {
            _chanceWizardBurned -= maxBurnReduction;
        } else {
            _chanceWizardBurned -= _wizardProficiency;
        }
        return _chanceWizardBurned;
    }

    function _chanceWizardDamaged(uint8 _wizardProficiency) private view returns(uint8) {
        return damagePercentChance - (_wizardProficiency * 2);
    }

    function healWithElixir(uint256 _tokenId, uint8 _quantity) external onlyEOA {
        uint256 _currentHP = tokenIdToInfo[_tokenId].hp;
        uint256 _maxHP = maxHPForToken(_tokenId);
        require(_currentHP > 0, "TrainingGame: HP must be greater than 0");
        require(_quantity > 0, "TrainingGame: Can't heal with 0 elixirs");
        require(_currentHP + _quantity <= _maxHP, "TrainingGame: Quantity is wrong");
        require(elixirOfHealingId > 0, "TrainingGame: Elixir of Healing ID not set");

        consumables.burn(elixirOfHealingId, _quantity, msg.sender);

        tokenIdToInfo[_tokenId].hp += _quantity;
    }

    function isWizardTraining(uint256 _tokenId) public view override returns(bool) {
        return tokenIdToInfo[_tokenId].requestId != 0;
    }

    function maxHPForToken(uint256 _tokenId) public view returns(uint8) {
        return baseHP + trainingProficiency.proficiencyForWizard(_tokenId);
    }

    function resetWizard(uint256 _tokenId) external override onlyAdminOrOwner {
        delete tokenIdToInfo[_tokenId];
        trainingProficiency.resetProficiencyForWizard(_tokenId);
    }

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "./ITrainingGame.sol";
import "./TrainingGameTimeKeeper.sol";

abstract contract TrainingGameArmor is Initializable, TrainingGameTimeKeeper {

    function __TrainingGameArmor_init() internal initializer {
        TrainingGameTimeKeeper.__TrainingGameTimeKeeper_init();
    }

    function hasArmor(uint256 _tokenId) public view returns(bool) {
        return tokenIdToInfo[_tokenId].hasArmor;
    }

    function equipArmor(uint256 _tokenId, address _owner) internal {
        consumables.safeTransferFrom(_owner, address(this), equipmentTokenId, 1, "");
        tokenIdToInfo[_tokenId].hasArmor = true;
    }

    function unequipArmor(uint256 _tokenId, address _owner) internal {
        require(tokenIdToInfo[_tokenId].hasArmor, "No armor equipped");
        consumables.safeTransferFrom(address(this), _owner, equipmentTokenId, 1, "");
        tokenIdToInfo[_tokenId].hasArmor = false;
    }

    function burnArmor(uint256 _tokenId) internal {
        require(tokenIdToInfo[_tokenId].hasArmor, "No armor equipped");
        consumables.burn(equipmentTokenId, 1, address(this));
        tokenIdToInfo[_tokenId].hasArmor = false;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "./ITrainingGame.sol";
import "./TrainingGameState.sol";
import "../../shared/Adminable.sol";
import "../../shared/randomizercl/IRandomizerCL.sol";
import "../world/IWorld.sol";
import "../tokens/sacrificialalter/ISacrificialAlter.sol";
import "../tokens/gp/IGP.sol";
import "../tokens/wnd/IWnD.sol";
import "../trainingproficiency/ITrainingProficiency.sol";

abstract contract TrainingGameContracts is Initializable, ITrainingGame, TrainingGameState {

    function __TrainingGameContracts_init() internal initializer {
        TrainingGameState.__TrainingGameState_init();
    }

    function setContracts(address _worldAddress, address _sacrificialAlterAddress, address _consumables, address _gpAddress, address _wndAddress, address _trainingProficiencyAddress, address _randomizerAddress, address _riftAddress, address _graveyard) external onlyAdminOrOwner {
        require(_worldAddress != address(0)
            && _gpAddress != address(0)
            && _wndAddress != address(0)
            && _trainingProficiencyAddress != address(0)
            && _randomizerAddress != address(0)
            && _sacrificialAlterAddress != address(0)
            && _consumables != address(0)
            && _riftAddress != address(0)
            && _graveyard != address(0), "Bad address.");

        world = IWorld(_worldAddress);
        sacrificialAlter = ISacrificialAlter(_sacrificialAlterAddress);
        gp = IGP(_gpAddress);
        wnd = IWnD(_wndAddress);
        trainingProficiency = ITrainingProficiency(_trainingProficiencyAddress);
        randomizer = IRandomizerCL(_randomizerAddress);
        rift = IRift(_riftAddress);
        consumables = IConsumables(_consumables);
        graveyard = IGraveyard(_graveyard);
    }

    modifier contractsAreSet() {
        require(areContractsSet(), "TrainingGame: Contracts not set");

        _;
    }

    function areContractsSet() public view returns(bool) {
        return address(world) != address(0)
            && address(gp) != address(0)
            && address(wnd) != address(0)
            && address(trainingProficiency) != address(0)
            && address(randomizer) != address(0)
            && address(sacrificialAlter) != address(0)
            && address(consumables) != address(0)
            && address(rift) != address(0)
            && address(graveyard) != address(0);
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/utils/structs/EnumerableSetUpgradeable.sol";

import "./TrainingGameContracts.sol";

abstract contract TrainingGameRewards is Initializable, TrainingGameContracts {
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.UintSet;

    function __TrainingGameRewards_init() internal initializer {
        TrainingGameContracts.__TrainingGameContracts_init();
    }

    // Clears existing reward Ids and replaces with the given reward ids.
    function setRewardSettings(uint256[] calldata _rewardIds, uint32[] calldata _rewardOdds, RewardMultiplier[] calldata _rewardMultipliers) external onlyAdminOrOwner {
        require(_rewardIds.length == _rewardOdds.length && _rewardOdds.length == _rewardMultipliers.length, "Invalid lengths");

        uint256[] memory rewardList = rewardIds.values();
        for(uint256 i = 0; i < rewardList.length; i++) {
            rewardIds.remove(rewardList[i]);
            delete rewardIdToOdds[rewardList[i]];
            delete rewardIdToMultiplier[rewardList[i]];
        }

        uint256 _oddsTotal;

        for(uint256 i = 0; i < _rewardIds.length; i++) {
            uint256 _newRewardId = _rewardIds[i];
            rewardIds.add(_newRewardId);
            _oddsTotal += _rewardOdds[i];
            rewardIdToOdds[_newRewardId] = _rewardOdds[i];
            rewardIdToMultiplier[_newRewardId] = _rewardMultipliers[i];
        }

        require(_oddsTotal == ODDS_TOTAL, "Bad odds");
    }

    // Clears existing reward Ids and replaces with the given reward ids.
    function setGPRewardSettings(uint256[] calldata _gpAmts, uint32[] calldata _gpOdds) external onlyAdminOrOwner {
        require(_gpAmts.length == _gpOdds.length, "Invalid lengths");
        
        uint256[] memory _gpAmtList = gpAmts.values();
        for(uint256 i = 0; i < _gpAmtList.length; i++) {
            gpAmts.remove(_gpAmtList[i]);
            delete gpAmtsToOdds[_gpAmtList[i]];
        }

        uint256 _oddsTotal = 0;

        for(uint256 i = 0; i < _gpAmts.length; i++) {
            uint256 _newGpAmt = _gpAmts[i];
            gpAmts.add(_newGpAmt);
            _oddsTotal += _gpOdds[i];
            gpAmtsToOdds[_newGpAmt] = _gpOdds[i];
        }

        require(_oddsTotal == ODDS_TOTAL, "Bad odds");
    }

    function pickReward(uint256 _random) internal view returns(uint256) {
        uint256 _result = _random % ODDS_TOTAL;

        uint32 _upperBound = 0;
        for(uint8 i = 0; i < rewardIds.length(); i++) {
            uint256 _rewardId = rewardIds.at(i);
            _upperBound += rewardIdToOdds[_rewardId];
            if(_result < _upperBound) {
                return _rewardId;
            }
        }

        revert("The odds are incorrect.");
    }

    function pickGPReward(uint256 _random) internal view returns(uint256) {
        uint256 _result = _random % ODDS_TOTAL;

        uint32 _upperBound = 0;
        for(uint8 i = 0; i < gpAmts.length(); i++) {
            uint256 _gpAmt = gpAmts.at(i);
            _upperBound += gpAmtsToOdds[_gpAmt];
            if(_result < _upperBound) {
                return _gpAmt;
            }
        }

        revert("The odds are incorrect.");
    }

    function pickAmount(uint256 _rewardId, uint256 _random, uint8 _wizardProficiency) internal view returns(uint16) {
        RewardMultiplier memory _multiplier = rewardIdToMultiplier[_rewardId];

        uint8 _multIndex = _wizardProficiency > 8 ? 8 : _wizardProficiency;
        uint16 _min = _multiplier.min[_multIndex];
        uint16 _max = _multiplier.max[_multIndex];

        if(_min == _max) {
            return _min;
        }

        uint16 _length = _max - _min + 1;
        uint256[] memory _odds = new uint256[](_length);

        // Every value between _min and _max does not have the same chance of being picked. Each higher value is half as likely as the one
        // before it.
        uint256 _currentOdd = 1;
        uint256 _maxRangeOdd = 0;
        for(uint16 i = _length - 1; i >= 0; i--) {
            _odds[i] = _currentOdd;
            _maxRangeOdd += _currentOdd;
            _currentOdd << 1;
        }

        uint256 _result = _random % _maxRangeOdd;

        uint256 _upperBound = 0;
        for(uint8 i = 0; i < _odds.length; i++) {
            _upperBound += _odds[i];
            if(_result < _upperBound) {
                return _min + i;
            }
        }

        revert("Odds broken");
    }

    modifier trainingRewardsSet() {
        require(rewardIds.length() > 0, "Rewards not set");

        _;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/utils/structs/EnumerableSetUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/utils/ERC721HolderUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC1155/utils/ERC1155HolderUpgradeable.sol";
import "../../shared/AdminableUpgradeable.sol";
import "./ITrainingGame.sol";
import "../../shared/Adminable.sol";
import "../../shared/randomizercl/IRandomizerCL.sol";
import "../world/IWorld.sol";
import "../rift/IRift.sol";
import "../graveyard/IGraveyard.sol";
import "../tokens/sacrificialalter/ISacrificialAlter.sol";
import "../tokens/consumables/IConsumables.sol";
import "../tokens/gp/IGP.sol";
import "../tokens/wnd/IWnD.sol";
import "../trainingproficiency/ITrainingProficiency.sol";

abstract contract TrainingGameState is Initializable, ERC721HolderUpgradeable, ERC1155HolderUpgradeable, AdminableUpgradeable {
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.UintSet;

    event TrainingStarted(address indexed _owner, uint256 indexed _tokenId, bytes32 indexed _requestId);
    event RewardMinted(address indexed _owner, uint256 indexed _tokenId, uint256 indexed _rewardId, uint256 _amount, uint256 _gpAmt);

    struct RewardMultiplier {
        // 9 possibilities for training proficiency... 0, 1... 8+
        uint16[9] min;
        uint16[9] max;
    }

    struct TokenTrainingInfo {
        bytes32 requestId;
        uint64 timeNextPlay;
        uint32 gamesPlayed;
        uint8 hp;
        bool hasArmor;
    }

    IWorld public world;
    ISacrificialAlter public sacrificialAlter;
    IConsumables public consumables;
    IGP public gp;
    IWnD public wnd;
    ITrainingProficiency public trainingProficiency;
    IRandomizerCL public randomizer;
    IRift public rift;
    IGraveyard public graveyard;

    uint256 public ODDS_TOTAL;
    uint256 internal equipmentTokenId;

    EnumerableSetUpgradeable.UintSet internal rewardIds;
    mapping(uint256 => uint32) internal rewardIdToOdds;
    mapping(uint256 => RewardMultiplier) internal rewardIdToMultiplier;

    EnumerableSetUpgradeable.UintSet internal gpAmts;
    mapping(uint256 => uint32) internal gpAmtsToOdds;

    mapping(uint256 => TokenTrainingInfo) public tokenIdToInfo;
    // array index is proficiency, and value at index is the number of games needed for that proficiency level.
    // Ex. proficiencyToGamesPlayed[1] = 6 means you get level 1 proficiency at 6 games.
    uint8[] internal proficiencyToGamesPlayed;
    uint256 public gameCooldownTime;

    uint256 public trainingFee;

    // The base chance, before training proficiency, that the wizard is burnt.
    uint8 public baseChanceWizardBurned;

    // The maximum the proficiency can reduce the chance of burning.
    uint8 public maxBurnReduction;

    uint8 public damagePercentChance;

    uint8 public baseHP;

    uint8 public baseDamage;

    uint8 public armorDamageReductionAmt;

    uint8 public elixirOfHealingId;

    function __TrainingGameState_init() internal initializer {
        AdminableUpgradeable.__Adminable_init();
        ERC721HolderUpgradeable.__ERC721Holder_init();
        ERC1155HolderUpgradeable.__ERC1155Holder_init();

        ODDS_TOTAL = 100000;
        gameCooldownTime = 1 days;
        trainingFee = 12000 ether;
        // 10% base chance of being burned in training
        baseChanceWizardBurned = 10;
        // 8% max burn reduction from training proficiency
        maxBurnReduction = 8;
        proficiencyToGamesPlayed = [0, 6, 8, 10, 12, 14, 16, 18, 20];
        equipmentTokenId = 2;
        damagePercentChance = 50;
        baseHP = 7;
        baseDamage = 6;
        armorDamageReductionAmt = 3;
        elixirOfHealingId = 1;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "./ITrainingGame.sol";
import "./TrainingGameState.sol";

abstract contract TrainingGameTimeKeeper is Initializable, ITrainingGame, TrainingGameState {

    function __TrainingGameTimeKeeper_init() internal initializer {
        TrainingGameState.__TrainingGameState_init();
    }

    function setGameCooldown(uint256 _gameCooldownTime) external onlyAdminOrOwner {
        gameCooldownTime = _gameCooldownTime;
    }

    function updateNextPlayTime(uint256 _tokenId, address _player) internal {
        // Add 24 hours and subtract 4 hours per Rift Tier
        tokenIdToInfo[_tokenId].timeNextPlay = uint64(block.timestamp + (24 * 3600) - (4 * rift.getRiftTier(_player) * 3600)); // 3600 seconds is an hour
    }

    function canWizardPlay(uint256 _tokenId) public view override returns(bool) {
        return block.timestamp > timeWizardCanPlayNext(_tokenId);
    }

    function timeWizardCanPlayNext(uint256 _tokenId) public view override returns(uint256) {
        return tokenIdToInfo[_tokenId].timeNextPlay;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface ITrainingProficiency {

    // Returns the proficiency for the given Wizard.
    function proficiencyForWizard(uint256 _tokenId) external view returns(uint8);

    // Increases the proficiency of the given wizard by 1.
    // Only admin.
    function increaseProficiencyForWizard(uint256 _tokenId) external;
    // Resets the proficiency of the given wizard.
    // Only admin.
    function resetProficiencyForWizard(uint256 _tokenId) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IWorldReadOnly {
    // Returns the total number of wizards staked somewhere in the world. Does not include in route wizards.
    function totalNumberOfWizards() external view returns(uint256);

    // Returns the total number of dragons staked somewhere in the world.
    function totalNumberOfDragons() external view returns(uint256);

    // Returns the location of the token. If it returns NONEXISTENT, the token is not staked in the world.
    function locationOfToken(uint256 _tokenId) external view returns(Location);

    // Returns if the token exists in the world. This also means the world contract holds the token.
    function isTokenInWorld(uint256 _tokenId) external view returns(bool);

    function getStakeableDragonLocations() external view returns(Location[] memory);

    function numberOfDragonsStakedAtRank(uint256 _rank) external view returns(uint256);

    // Returns the number of dragons that are staked at the given location and rank.
    function numberOfDragonsStakedAtLocationAtRank(Location _location, uint256 _rank) external view returns(uint256);

    // Returns the number of wizards that are staked at the given location.
    function numberOfWizardsStakedAtLocation(Location _location) external view returns(uint256);

    // Returns the dragon ID that is at the given location at the given index. Will revert if invalid index.
    function dragonAtLocationAtRankAtIndex(Location _location, uint256 _rank, uint256 _index) external view returns(uint256);

    // Returns the wizard ID that is at the given location at the given index. Will revert if invalid index.
    function wizardAtLocationAtIndex(Location _location, uint256 _index) external view returns(uint256);

    // Returns all dragons at the given location for the given owner. Avoid using in a contract-to-contract call.
    function getDragonsAtLocationForOwner(Location _location, address _owner) external view returns(uint256[] memory);

    // Returns all dragons at the given location for the given owner. Avoid using in a contract-to-contract call.
    function getWizardsAtLocationForOwner(Location _location, address _owner) external view returns(uint256[] memory);

    // The owner of the given token.
    function ownerOfTokenId(uint256 _tokenId) external view returns(address);

    // Returns if the passed in address is the owner of the token id.
    function isOwnerOfTokenId(uint256 _tokenId, address _owner) external view returns(bool);

    // Returns a random dragon owner based on the given seed.
    // Dragons that are staked at the given location have an increased odds of being selected.
    // If _locationOfEvent is set to NONEXISTENT, all dragons staked in the world will have the same odds.
    // If this function returns 0, there was not a random dragon staked.
    function getRandomDragonOwner(uint256 _randomSeed, Location _locationOfEvent) external view returns(address);
}

interface IWorldEditable {

    // Begins staking the given wizard at the given location. Must be approved to transfer from their wallet to this contract.
    // May revert for various reasons.
    function startStakeWizards(uint256[] calldata _tokenIds, Location _location) external;

    // Finishes the stake for the given wizard ID. Must be called after the random has been seeded.
    function finishStakeWizards(uint256[] calldata _tokenIds, Location _location) external;

    // Unstakes the given wizard from the given location.
    // May revert for various reasons.
    function startUnstakeWizards(uint256[] calldata _tokenIds, Location _location) external;

    // Finishes the unstake process for the given wizard id. Must be called after the random has been seeded.
    function finishUnstakeWizards(uint256[] calldata _tokenIds, Location _location) external;

    // Stakes the given dragon at the given location. Must be approved to transfer from their wallet to this contract.
    // May revert for various reasons.
    function stakeDragons(uint256[] calldata _tokenIds, Location _location) external;

    // Unstakes the given dragon at the given location. Must be approved to transfer from their wallet to this contract.
    // May revert for various reasons.
    function unstakeDragons(uint256[] calldata _tokenIds, Location _location) external;

    // When calling, should already have ensured this is a wizard and is not in the world already.
    // Transfers the 721 to the world contract.
    // Admin only.
    function addWizardToWorld(uint256 _tokenId, address _owner, Location _location) external;

    // When calling, should already have ensured this is a wizard and is not in the world already.
    // Transfers the 721 to the world contract.
    // Admin only.
    function addDragonToWorld(uint256 _tokenId, address _owner, Location _location) external;

    // Game logic should already validate that this is an option.
    // Transfers the 721 to the _owner.
    // Admin only.
    function removeWizardFromWorld(uint256 _tokenId, address _owner) external;

    // Game logic should already validate that this is an option.
    // Transfers the 721 to the _owner.
    // Admin only.
    function removeDragonFromWorld(uint256 _tokenId, address _owner) external;

    // When calling, game logic should already validate who owns the token, if they have permission, and that
    // the destination location makes sense.
    // Only callable by admin/owner.
    function changeLocationOfWizard(uint256 _tokenId, Location _location) external;

    function setStakeableDragonLocations(Location[] calldata _locations) external;
}

interface IWorld is IWorldEditable, IWorldReadOnly {

}

enum Location {
    NONEXISTENT,
    RIFT,
    TRAINING_GROUNDS_ENTERING,
    TRAINING_GROUNDS,
    TRAINING_GROUNDS_LEAVING
}