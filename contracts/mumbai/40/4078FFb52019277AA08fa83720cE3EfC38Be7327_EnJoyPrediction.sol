// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface AggregatorV3Interface {
  function decimals() external view returns (uint8);

  function description() external view returns (string memory);

  function version() external view returns (uint256);

  function getRoundData(uint80 _roundId)
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );

  function latestRoundData()
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 *
 * [WARNING]
 * ====
 *  Trying to delete such a structure from storage will likely result in data corruption, rendering the structure unusable.
 *  See https://github.com/ethereum/solidity/pull/11843[ethereum/solidity#11843] for more info.
 *
 *  In order to clean an EnumerableSet, you can either remove all elements one by one or create a fresh instance using an array of EnumerableSet.
 * ====
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastValue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastValue;
                // Update the index for the moved value
                set._indexes[lastValue] = valueIndex; // Replace lastValue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";
import "./utils/EnumerableMap.sol";

/**
 * @author InJoy Labs (https://injoylabs.io/)
 * @title A game about predicting the price of BTC
 */
contract EnJoyPrediction {
    // uint-to-uint enumerable map
    using EnumerableMap for EnumerableMap.U2UMap;

    uint32 private constant MIN_STAKE = 1_000_000; // 1 USDT

    uint32 private constant MAX_STAKE = 5_000_000; // 5 USDT

    uint32 private constant DAY_TIME_OFFSET = 11 * 60 * 60; // 7 p.m. UTC+8

    /// @dev Player's stake info given table
    struct StakeInfo {
        uint32 stakeAmount;
        uint8 prediction;
    }

    /// @dev Table result state
    enum TableResult {
        NULL,
        LONG,
        SHORT,
        DRAW
    }

    /// @dev Table's info given table ID (global storage)
    struct TableInfo {
        TableResult result;
        uint64 startPrice;
        uint80 longPool;
        uint80 shortPool;
        uint24 playerCount;
    }

    /// @dev USDT contract
    IERC20 private immutable _usdt;

    /// @dev Chainlink BTC price feed oracle
    AggregatorV3Interface private immutable _btcPriceFeed;

    /// @dev Player => (TableID => StakeInfo)
    mapping(address => EnumerableMap.U2UMap) private _stakeInfoMapOf;

    /// @dev TableID => TableInfo
    mapping(uint256 => TableInfo) private _tableInfoMap;

    /// @dev Connect USDT contract and BTC oracle
    constructor(IERC20 usdtAddress, AggregatorV3Interface btcAggregator) {
        _usdt = usdtAddress;
        _btcPriceFeed = btcAggregator;
    }

    /**
     * Execution Functions
     */

    /// @dev Predict BTC price with certain amount of USDT
    function predict(bool predictLong, uint32 stakeAmount) public {
        uint256 tableId = _getCurrentTableId();
        // checks
        require(
            stakeAmount <= MAX_STAKE && stakeAmount >= MIN_STAKE,
            "stake out of range"
        );
        require(
            !_stakeInfoMapOf[msg.sender].contains(tableId),
            "already predicted"
        );

        // get current table info
        TableInfo storage tableInfo = _tableInfoMap[tableId];

        // stake USDT to contract
        _usdt.transferFrom(msg.sender, address(this), stakeAmount);

        // update table info
        if (predictLong) tableInfo.longPool += stakeAmount;
        else tableInfo.shortPool += stakeAmount;

        // add stake info to player's profolio
        uint256 serialNumber = _serializeStakeInfo(
            StakeInfo(
                stakeAmount,
                predictLong ? uint8(TableResult.LONG) : uint8(TableResult.SHORT)
            )
        );
        _stakeInfoMapOf[msg.sender].set(tableId, serialNumber);

        // increase player count by one
        ++tableInfo.playerCount;
    }

    /// @dev Claim reward given table IDs
    function claim() public {
        EnumerableMap.U2UMap storage stakeInfoMap = _stakeInfoMapOf[msg.sender];
        uint256 mapSize = stakeInfoMap.length();
        uint80 claimableReward = 0;
        for (uint256 i = 0; i < mapSize; ++i) {
            (uint256 tableId, uint256 serialNumber) = stakeInfoMap.at(i);
            StakeInfo memory stakeInfo = _deserializeStakeInfo(serialNumber);
            TableInfo memory tableInfo = _tableInfoMap[tableId];
            if (tableInfo.result == TableResult.DRAW) {
                claimableReward += stakeInfo.stakeAmount;
                stakeInfoMap.remove(tableId);
            } else {
                uint80 pool = tableInfo.longPool + tableInfo.shortPool;
                uint80 shares = tableInfo.result == TableResult.LONG
                    ? tableInfo.longPool
                    : tableInfo.shortPool;
                if (stakeInfo.prediction == uint8(tableInfo.result)) {
                    claimableReward += (stakeInfo.stakeAmount * pool) / shares;
                    stakeInfoMap.remove(tableId);
                }
            }
        }

        // transfer reward to player
        _usdt.transfer(msg.sender, (claimableReward * 99) / 100);
    }

    /// @dev Settle the result using Chainlink oracle
    function settle() public {
        uint256 tableId = _getCurrentTableId();
        TableInfo storage stakingTableInfo = _tableInfoMap[tableId - 1];
        TableInfo storage waitingTableInfo = _tableInfoMap[tableId - 2];

        // check if current table has already created
        require(stakingTableInfo.startPrice == 0, "settle too early");

        // fetch BTC price from Chainlink oracle
        (, int256 price, , , ) = _btcPriceFeed.latestRoundData();
        uint64 currPrice = uint64(uint256(price));

        // set the start price of current table
        stakingTableInfo.startPrice = currPrice;

        // settle the result of waiting table
        if (waitingTableInfo.longPool == 0 || waitingTableInfo.shortPool == 0) {
            waitingTableInfo.result = TableResult.DRAW;
        } else {
            uint64 previousStartPrice = waitingTableInfo.startPrice;
            if (currPrice > previousStartPrice) {
                waitingTableInfo.result = TableResult.LONG;
            } else if (currPrice < previousStartPrice) {
                waitingTableInfo.result = TableResult.SHORT;
            } else {
                waitingTableInfo.result = TableResult.DRAW;
            }
        }

        // transfer reward to settler to covering the gas fee
        uint80 reward = (waitingTableInfo.longPool +
            waitingTableInfo.longPool) / 100;
        _usdt.transfer(msg.sender, reward);
    }

    /**
     * Query Functions
     */

    /// @dev compute unclaimed reward of certain player
    function getPlayerUnclaimReward(address player)
        public
        view
        returns (uint80 claimableReward)
    {
        EnumerableMap.U2UMap storage stakeInfoMap = _stakeInfoMapOf[player];
        uint256 mapSize = stakeInfoMap.length();
        claimableReward = 0;
        for (uint256 i = 0; i < mapSize; ++i) {
            (uint256 tableId, uint256 serialNumber) = stakeInfoMap.at(i);
            StakeInfo memory stakeInfo = _deserializeStakeInfo(serialNumber);
            TableInfo memory tableInfo = _tableInfoMap[tableId];
            if (tableInfo.result == TableResult.DRAW) {
                claimableReward += stakeInfo.stakeAmount;
            } else {
                uint80 pool = tableInfo.longPool + tableInfo.shortPool;
                uint80 shares = tableInfo.result == TableResult.LONG
                    ? tableInfo.longPool
                    : tableInfo.shortPool;
                if (stakeInfo.prediction == uint8(tableInfo.result))
                    claimableReward += (stakeInfo.stakeAmount * pool) / shares;
            }
        }
    }

    /// @dev get current stake for long and short
    function getTableInfo(uint256 timestamp)
        public
        view
        returns (TableInfo memory)
    {
        uint256 tableId = _getTableId(timestamp);
        return _tableInfoMap[tableId];
    }

    /// @dev get current stake info of certain player
    function getPlayerStakeInfo(address player, uint256 timestamp)
        public
        view
        returns (StakeInfo memory)
    {
        uint256 tableId = _getTableId(timestamp);
        (bool ifPredicted, uint256 serialNumber) = _stakeInfoMapOf[player]
            .tryGet(tableId);
        // return stake info if predicted
        if (ifPredicted) {
            return _deserializeStakeInfo(serialNumber);
        } else {
            return StakeInfo(0, 0);
        }
    }

    /// @dev deserialize uint256 to StakeInfo struct
    function _deserializeStakeInfo(uint256 serialNumber)
        private
        pure
        returns (StakeInfo memory stakeInfo)
    {
        stakeInfo.prediction = uint8(serialNumber);
        serialNumber >>= 8;
        stakeInfo.stakeAmount = uint32(serialNumber);
    }

    /// @dev serialize StakeInfo struct into uint256
    function _serializeStakeInfo(StakeInfo memory stakeInfo)
        private
        pure
        returns (uint256 serialNumber)
    {
        serialNumber = uint256(stakeInfo.stakeAmount);
        serialNumber = (serialNumber << 8) | stakeInfo.prediction;
    }

    /// @dev get current table ID = cumulative days from timestamp 0
    function _getCurrentTableId() private view returns (uint256) {
        return _getTableId(block.timestamp);
    }

    /// @dev get table ID given timestamp
    function _getTableId(uint256 timestamp) private pure returns (uint256) {
        return (timestamp - DAY_TIME_OFFSET) / (1 days);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableMap.sol)

pragma solidity ^0.8.13;

import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

/**
 * @dev Same as openzeppelin one but with uint-to-uint Implementation
 */
library EnumerableMap {
    using EnumerableSet for EnumerableSet.Bytes32Set;

    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Map type with
    // bytes32 keys and values.
    // The Map implementation uses private functions, and user-facing
    // implementations (such as Uint256ToUint256Map) are just wrappers around
    // the underlying Map.
    // This means that we can only create new EnumerableMaps for types that fit
    // in bytes32.

    struct Map {
        // Storage of keys
        EnumerableSet.Bytes32Set _keys;
        mapping(bytes32 => bytes32) _values;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function _set(
        Map storage map,
        bytes32 key,
        bytes32 value
    ) private returns (bool) {
        map._values[key] = value;
        return map._keys.add(key);
    }

    /**
     * @dev Removes a key-value pair from a map. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function _remove(Map storage map, bytes32 key) private returns (bool) {
        delete map._values[key];
        return map._keys.remove(key);
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function _contains(Map storage map, bytes32 key)
        private
        view
        returns (bool)
    {
        return map._keys.contains(key);
    }

    /**
     * @dev Returns the number of key-value pairs in the map. O(1).
     */
    function _length(Map storage map) private view returns (uint256) {
        return map._keys.length();
    }

    /**
     * @dev Returns the key-value pair stored at position `index` in the map. O(1).
     *
     * Note that there are no guarantees on the ordering of entries inside the
     * array, and it may change when more entries are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Map storage map, uint256 index)
        private
        view
        returns (bytes32, bytes32)
    {
        bytes32 key = map._keys.at(index);
        return (key, map._values[key]);
    }

    /**
     * @dev Tries to returns the value associated with `key`.  O(1).
     * Does not revert if `key` is not in the map.
     */
    function _tryGet(Map storage map, bytes32 key)
        private
        view
        returns (bool, bytes32)
    {
        bytes32 value = map._values[key];
        if (value == bytes32(0)) {
            return (_contains(map, key), bytes32(0));
        } else {
            return (true, value);
        }
    }

    /**
     * @dev Returns the value associated with `key`.  O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function _get(Map storage map, bytes32 key) private view returns (bytes32) {
        bytes32 value = map._values[key];
        require(
            value != 0 || _contains(map, key),
            "EnumerableMap: nonexistent key"
        );
        return value;
    }

    /**
     * @dev Same as {_get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {_tryGet}.
     */
    function _get(
        Map storage map,
        bytes32 key,
        string memory errorMessage
    ) private view returns (bytes32) {
        bytes32 value = map._values[key];
        require(value != 0 || _contains(map, key), errorMessage);
        return value;
    }

    // U2UMap

    struct U2UMap {
        Map _inner;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(
        U2UMap storage map,
        uint256 key,
        uint256 value
    ) internal returns (bool) {
        return _set(map._inner, bytes32(key), bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(U2UMap storage map, uint256 key) internal returns (bool) {
        return _remove(map._inner, bytes32(key));
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(U2UMap storage map, uint256 key)
        internal
        view
        returns (bool)
    {
        return _contains(map._inner, bytes32(key));
    }

    /**
     * @dev Returns the number of elements in the map. O(1).
     */
    function length(U2UMap storage map) internal view returns (uint256) {
        return _length(map._inner);
    }

    /**
     * @dev Returns the element stored at position `index` in the set. O(1).
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(U2UMap storage map, uint256 index)
        internal
        view
        returns (uint256, uint256)
    {
        (bytes32 key, bytes32 value) = _at(map._inner, index);
        return (uint256(key), uint256(value));
    }

    /**
     * @dev Tries to returns the value associated with `key`.  O(1).
     * Does not revert if `key` is not in the map.
     *
     * _Available since v3.4._
     */
    function tryGet(U2UMap storage map, uint256 key)
        internal
        view
        returns (bool, uint256)
    {
        (bool success, bytes32 value) = _tryGet(map._inner, bytes32(key));
        return (success, uint256(value));
    }

    /**
     * @dev Returns the value associated with `key`.  O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(U2UMap storage map, uint256 key)
        internal
        view
        returns (uint256)
    {
        return uint256(_get(map._inner, bytes32(key)));
    }
}